<!DOCTYPE html>
<html>
<head>
  <meta charset="utf-8"/>
  <style>
    table.head, table.foot { width: 100%; }
    td.head-rtitle, td.foot-os { text-align: right; }
    td.head-vol { text-align: center; }
    div.Pp { margin: 1ex 0ex; }
  </style>
  <title>PERLFAQ7(1)</title>
</head>
<body>
<table class="head">
  <tr>
    <td class="head-ltitle">PERLFAQ7(1)</td>
    <td class="head-vol">Perl Programmers Reference Guide</td>
    <td class="head-rtitle">PERLFAQ7(1)</td>
  </tr>
</table>
<div class="manual-text">
<h1 class="Sh" title="Sh" id="NAME"><a class="selflink" href="#NAME">NAME</a></h1>
perlfaq7 - General Perl Language Issues
<h1 class="Sh" title="Sh" id="DESCRIPTION"><a class="selflink" href="#DESCRIPTION">DESCRIPTION</a></h1>
This section deals with general Perl language issues that don't clearly fit into
  any of the other sections.
<h2 class="Ss" title="Ss" id="Can_I_get_a_BNF/yacc/RE_for_the_Perl_language?"><a class="selflink" href="#Can_I_get_a_BNF/yacc/RE_for_the_Perl_language?">Can
  I get a BNF/yacc/RE for the Perl language?</a></h2>
There is no BNF, but you can paw your way through the yacc grammar in perly.y in
  the source distribution if you're particularly brave. The grammar relies on
  very smart tokenizing code, so be prepared to venture into toke.c as well.
<div class="Pp"></div>
In the words of Chaim Frenkel: &quot;Perl's grammar can not be reduced to BNF.
  The work of parsing perl is distributed between yacc, the lexer, smoke and
  mirrors.&quot;
<h2 class="Ss" title="Ss" id="What_are_all_these_$@%&amp;*_punctuation_signs,_and_how_do_I_know_when_to_use_them?"><a class="selflink" href="#What_are_all_these_$@%&amp;*_punctuation_signs,_and_how_do_I_know_when_to_use_them?">What
  are all these $@%&amp;* punctuation signs, and how do I know when to use
  them?</a></h2>
They are type specifiers, as detailed in perldata:
<div class="Pp"></div>
<pre>
    $ for scalar values (number, string or reference)
    @ for arrays
    % for hashes (associative arrays)
    &amp; for subroutines (aka functions, procedures, methods)
    * for all types of that symbol name. In version 4 you used them like
      pointers, but in modern perls you can just use references.
</pre>
<div class="Pp"></div>
There are a couple of other symbols that you're likely to encounter that aren't
  really type specifiers:
<div class="Pp"></div>
<pre>
    &lt;&gt; are used for inputting a record from a filehandle.
    \  takes a reference to something.
</pre>
<div class="Pp"></div>
Note that &lt;FILE&gt; is <i>neither</i> the type specifier for files nor the
  name of the handle. It is the &quot;&lt;&gt;&quot; operator applied to the
  handle FILE. It reads one line (well, record--see &quot;$/&quot; in perlvar)
  from the handle FILE in scalar context, or <i>all</i> lines in list context.
  When performing open, close, or any other operation besides
  &quot;&lt;&gt;&quot; on files, or even when talking about the handle, do
  <i>not</i> use the brackets. These are correct: &quot;eof(FH)&quot;,
  &quot;seek(FH, 0, 2)&quot; and &quot;copying from STDIN to FILE&quot;.
<h2 class="Ss" title="Ss" id="Do_I_always/never_have_to_quote_my_strings_or_use_semicolons_and_commas?"><a class="selflink" href="#Do_I_always/never_have_to_quote_my_strings_or_use_semicolons_and_commas?">Do
  I always/never have to quote my strings or use semicolons and commas?</a></h2>
Normally, a bareword doesn't need to be quoted, but in most cases probably
  should be (and must be under &quot;use strict&quot;). But a hash key
  consisting of a simple word and the left-hand operand to the &quot;=&gt;&quot;
  operator both count as though they were quoted:
<div class="Pp"></div>
<pre>
    This                    is like this
    ------------            ---------------
    $foo{line}              $foo{'line'}
    bar =&gt; stuff            'bar' =&gt; stuff
</pre>
<div class="Pp"></div>
The final semicolon in a block is optional, as is the final comma in a list.
  Good style (see perlstyle) says to put them in except for one-liners:
<div class="Pp"></div>
<pre>
    if ($whoops) { exit 1 }
    my @nums = (1, 2, 3);
    if ($whoops) {
        exit 1;
    }
    my @lines = (
        &quot;There Beren came from mountains cold&quot;,
        &quot;And lost he wandered under leaves&quot;,
    );
</pre>
<h2 class="Ss" title="Ss" id="How_do_I_skip_some_return_values?"><a class="selflink" href="#How_do_I_skip_some_return_values?">How
  do I skip some return values?</a></h2>
One way is to treat the return values as a list and index into it:
<div class="Pp"></div>
<pre>
    $dir = (getpwnam($user))[7];
</pre>
<div class="Pp"></div>
Another way is to use undef as an element on the left-hand-side:
<div class="Pp"></div>
<pre>
    ($dev, $ino, undef, undef, $uid, $gid) = stat($file);
</pre>
<div class="Pp"></div>
You can also use a list slice to select only the elements that you need:
<div class="Pp"></div>
<pre>
    ($dev, $ino, $uid, $gid) = ( stat($file) )[0,1,4,5];
</pre>
<h2 class="Ss" title="Ss" id="How_do_I_temporarily_block_warnings?"><a class="selflink" href="#How_do_I_temporarily_block_warnings?">How
  do I temporarily block warnings?</a></h2>
If you are running Perl 5.6.0 or better, the &quot;use warnings&quot; pragma
  allows fine control of what warnings are produced. See perllexwarn for more
  details.
<div class="Pp"></div>
<pre>
    {
        no warnings;          # temporarily turn off warnings
        $x = $y + $z;         # I know these might be undef
    }
</pre>
<div class="Pp"></div>
Additionally, you can enable and disable categories of warnings. You turn off
  the categories you want to ignore and you can still get other categories of
  warnings. See perllexwarn for the complete details, including the category
  names and hierarchy.
<div class="Pp"></div>
<pre>
    {
        no warnings 'uninitialized';
        $x = $y + $z;
    }
</pre>
<div class="Pp"></div>
If you have an older version of Perl, the $^W variable (documented in perlvar)
  controls runtime warnings for a block:
<div class="Pp"></div>
<pre>
    {
        local $^W = 0;        # temporarily turn off warnings
        $x = $y + $z;         # I know these might be undef
    }
</pre>
<div class="Pp"></div>
Note that like all the punctuation variables, you cannot currently use
  <i>my()</i> on $^W, only <i>local()</i>.
<h2 class="Ss" title="Ss" id="What's_an_extension?"><a class="selflink" href="#What's_an_extension?">What's
  an extension?</a></h2>
An extension is a way of calling compiled C code from Perl. Reading perlxstut is
  a good place to learn more about extensions.
<h2 class="Ss" title="Ss" id="Why_do_Perl_operators_have_different_precedence_than_C_operators?"><a class="selflink" href="#Why_do_Perl_operators_have_different_precedence_than_C_operators?">Why
  do Perl operators have different precedence than C operators?</a></h2>
Actually, they don't. All C operators that Perl copies have the same precedence
  in Perl as they do in C. The problem is with operators that C doesn't have,
  especially functions that give a list context to everything on their right,
  eg. print, chmod, exec, and so on. Such functions are called &quot;list
  operators&quot; and appear as such in the precedence table in perlop.
<div class="Pp"></div>
A common mistake is to write:
<div class="Pp"></div>
<pre>
    unlink $file || die &quot;snafu&quot;;
</pre>
<div class="Pp"></div>
This gets interpreted as:
<div class="Pp"></div>
<pre>
    unlink ($file || die &quot;snafu&quot;);
</pre>
<div class="Pp"></div>
To avoid this problem, either put in extra parentheses or use the super low
  precedence &quot;or&quot; operator:
<div class="Pp"></div>
<pre>
    (unlink $file) || die &quot;snafu&quot;;
    unlink $file or die &quot;snafu&quot;;
</pre>
<div class="Pp"></div>
The &quot;English&quot; operators (&quot;and&quot;, &quot;or&quot;,
  &quot;xor&quot;, and &quot;not&quot;) deliberately have precedence lower than
  that of list operators for just such situations as the one above.
<div class="Pp"></div>
Another operator with surprising precedence is exponentiation. It binds more
  tightly even than unary minus, making &quot;-2**2&quot; produce a negative
  four and not a positive one. It is also right-associating, meaning that
  &quot;2**3**2&quot; is two raised to the ninth power, not eight squared.
<div class="Pp"></div>
Although it has the same precedence as in C, Perl's &quot;?:&quot; operator
  produces an lvalue. This assigns $x to either $if_true or $if_false, depending
  on the trueness of $maybe:
<div class="Pp"></div>
<pre>
    ($maybe ? $if_true : $if_false) = $x;
</pre>
<h2 class="Ss" title="Ss" id="How_do_I_declare/create_a_structure?"><a class="selflink" href="#How_do_I_declare/create_a_structure?">How
  do I declare/create a structure?</a></h2>
In general, you don't &quot;declare&quot; a structure. Just use a (probably
  anonymous) hash reference. See perlref and perldsc for details. Here's an
  example:
<div class="Pp"></div>
<pre>
    $person = {};                   # new anonymous hash
    $person-&gt;{AGE}  = 24;           # set field AGE to 24
    $person-&gt;{NAME} = &quot;Nat&quot;;        # set field NAME to &quot;Nat&quot;
</pre>
<div class="Pp"></div>
If you're looking for something a bit more rigorous, try perltoot.
<h2 class="Ss" title="Ss" id="How_do_I_create_a_module?"><a class="selflink" href="#How_do_I_create_a_module?">How
  do I create a module?</a></h2>
perlnewmod is a good place to start, ignore the bits about uploading to CPAN if
  you don't want to make your module publicly available.
<div class="Pp"></div>
ExtUtils::ModuleMaker and Module::Starter are also good places to start. Many
  CPAN authors now use Dist::Zilla to automate as much as possible.
<div class="Pp"></div>
Detailed documentation about modules can be found at: perlmod, perlmodlib,
  perlmodstyle.
<div class="Pp"></div>
If you need to include C code or C library interfaces use h2xs. h2xs will create
  the module distribution structure and the initial interface files. perlxs and
  perlxstut explain the details.
<h2 class="Ss" title="Ss" id="How_do_I_adopt_or_take_over_a_module_already_on_CPAN?"><a class="selflink" href="#How_do_I_adopt_or_take_over_a_module_already_on_CPAN?">How
  do I adopt or take over a module already on CPAN?</a></h2>
Ask the current maintainer to make you a co-maintainer or transfer the module to
  you.
<div class="Pp"></div>
If you can not reach the author for some reason contact the PAUSE admins at
  modules@perl.org who may be able to help, but each case it treated seperatly.
<dl class="Bl-tag">
  <dt class="It-tag">&#x2022;</dt>
  <dd class="It-tag">Get a login for the Perl Authors Upload Server (PAUSE) if
      you don't already have one: &lt;http://pause.perl.org&gt;</dd>
</dl>
<dl class="Bl-tag">
  <dt class="It-tag">&#x2022;</dt>
  <dd class="It-tag">Write to modules@perl.org explaining what you did to
      contact the current maintainer. The PAUSE admins will also try to reach
      the maintainer.</dd>
</dl>
<dl class="Bl-tag">
  <dt class="It-tag">&#x2022;</dt>
  <dd class="It-tag">Post a public message in a heavily trafficked site
      announcing your intention to take over the module.</dd>
</dl>
<dl class="Bl-tag">
  <dt class="It-tag">&#x2022;</dt>
  <dd class="It-tag">Wait a bit. The PAUSE admins don't want to act too quickly
      in case the current maintainer is on holiday. If there's no response to
      private communication or the public post, a PAUSE admin can transfer it to
      you.</dd>
</dl>
<h2 class="Ss" title="Ss" id="How_do_I_create_a_class?"><a class="selflink" href="#How_do_I_create_a_class?">How
  do I create a class?</a></h2>
(contributed by brian d foy)
<div class="Pp"></div>
In Perl, a class is just a package, and methods are just subroutines. Perl
  doesn't get more formal than that and lets you set up the package just the way
  that you like it (that is, it doesn't set up anything for you).
<div class="Pp"></div>
The Perl documentation has several tutorials that cover class creation,
  including perlboot (Barnyard Object Oriented Tutorial), perltoot (Tom's Object
  Oriented Tutorial), perlbot (Bag o' Object Tricks), and perlobj.
<h2 class="Ss" title="Ss" id="How_can_I_tell_if_a_variable_is_tainted?"><a class="selflink" href="#How_can_I_tell_if_a_variable_is_tainted?">How
  can I tell if a variable is tainted?</a></h2>
You can use the <i>tainted()</i> function of the Scalar::Util module, available
  from CPAN (or included with Perl since release 5.8.0). See also
  &quot;Laundering and Detecting Tainted Data&quot; in perlsec.
<h2 class="Ss" title="Ss" id="What's_a_closure?"><a class="selflink" href="#What's_a_closure?">What's
  a closure?</a></h2>
Closures are documented in perlref.
<div class="Pp"></div>
<i>Closure</i> is a computer science term with a precise but hard-to-explain
  meaning. Usually, closures are implemented in Perl as anonymous subroutines
  with lasting references to lexical variables outside their own scopes. These
  lexicals magically refer to the variables that were around when the subroutine
  was defined (deep binding).
<div class="Pp"></div>
Closures are most often used in programming languages where you can have the
  return value of a function be itself a function, as you can in Perl. Note that
  some languages provide anonymous functions but are not capable of providing
  proper closures: the Python language, for example. For more information on
  closures, check out any textbook on functional programming. Scheme is a
  language that not only supports but encourages closures.
<div class="Pp"></div>
Here's a classic non-closure function-generating function:
<div class="Pp"></div>
<pre>
    sub add_function_generator {
        return sub { shift() + shift() };
    }
    my $add_sub = add_function_generator();
    my $sum = $add_sub-&gt;(4,5);                # $sum is 9 now.
</pre>
<div class="Pp"></div>
The anonymous subroutine returned by <i>add_function_generator()</i> isn't
  technically a closure because it refers to no lexicals outside its own scope.
  Using a closure gives you a <i>function template</i> with some customization
  slots left out to be filled later.
<div class="Pp"></div>
Contrast this with the following <i>make_adder()</i> function, in which the
  returned anonymous function contains a reference to a lexical variable outside
  the scope of that function itself. Such a reference requires that Perl return
  a proper closure, thus locking in for all time the value that the lexical had
  when the function was created.
<div class="Pp"></div>
<pre>
    sub make_adder {
        my $addpiece = shift;
        return sub { shift() + $addpiece };
    }
    my $f1 = make_adder(20);
    my $f2 = make_adder(555);
</pre>
<div class="Pp"></div>
Now &quot;$f1-&gt;($n)&quot; is always 20 plus whatever $n you pass in, whereas
  &quot;$f2-&gt;($n)&quot; is always 555 plus whatever $n you pass in. The
  $addpiece in the closure sticks around.
<div class="Pp"></div>
Closures are often used for less esoteric purposes. For example, when you want
  to pass in a bit of code into a function:
<div class="Pp"></div>
<pre>
    my $line;
    timeout( 30, sub { $line = &lt;STDIN&gt; } );
</pre>
<div class="Pp"></div>
If the code to execute had been passed in as a string, '$line = &lt;STDIN&gt;',
  there would have been no way for the hypothetical <i>timeout()</i> function to
  access the lexical variable $line back in its caller's scope.
<div class="Pp"></div>
Another use for a closure is to make a variable <i>private</i> to a named
  subroutine, e.g. a counter that gets initialized at creation time of the sub
  and can only be modified from within the sub. This is sometimes used with a
  BEGIN block in package files to make sure a variable doesn't get meddled with
  during the lifetime of the package:
<div class="Pp"></div>
<pre>
    BEGIN {
        my $id = 0;
        sub next_id { ++$id }
    }
</pre>
<div class="Pp"></div>
This is discussed in more detail in perlsub; see the entry on <i>Persistent
  Private Variables</i>.
<h2 class="Ss" title="Ss" id="What_is_variable_suicide_and_how_can_I_prevent_it?"><a class="selflink" href="#What_is_variable_suicide_and_how_can_I_prevent_it?">What
  is variable suicide and how can I prevent it?</a></h2>
This problem was fixed in perl 5.004_05, so preventing it means upgrading your
  version of perl. ;)
<div class="Pp"></div>
Variable suicide is when you (temporarily or permanently) lose the value of a
  variable. It is caused by scoping through <i>my()</i> and <i>local()</i>
  interacting with either closures or aliased <i>foreach()</i> iterator
  variables and subroutine arguments. It used to be easy to inadvertently lose a
  variable's value this way, but now it's much harder. Take this code:
<div class="Pp"></div>
<pre>
    my $f = 'foo';
    sub T {
        while ($i++ &lt; 3) { my $f = $f; $f .= &quot;bar&quot;; print $f, &quot;\n&quot; }
    }
    T;
    print &quot;Finally $f\n&quot;;
</pre>
<div class="Pp"></div>
If you are experiencing variable suicide, that &quot;my $f&quot; in the
  subroutine doesn't pick up a fresh copy of the $f whose value is 'foo'. The
  output shows that inside the subroutine the value of $f leaks through when it
  shouldn't, as in this output:
<div class="Pp"></div>
<pre>
    foobar
    foobarbar
    foobarbarbar
    Finally foo
</pre>
<div class="Pp"></div>
The $f that has &quot;bar&quot; added to it three times should be a new $f
  &quot;my $f&quot; should create a new lexical variable each time through the
  loop. The expected output is:
<div class="Pp"></div>
<pre>
    foobar
    foobar
    foobar
    Finally foo
</pre>
<h2 class="Ss" title="Ss" id="How_can_I_pass/return_a_{Function,_FileHandle,_Array,_Hash,_Method,_Regex}?"><a class="selflink" href="#How_can_I_pass/return_a_{Function,_FileHandle,_Array,_Hash,_Method,_Regex}?">How
  can I pass/return a {Function, FileHandle, Array, Hash, Method,
  Regex}?</a></h2>
You need to pass references to these objects. See &quot;Pass by Reference&quot;
  in perlsub for this particular question, and perlref for information on
  references.
<dl class="Bl-tag">
  <dt class="It-tag">Passing Variables and Functions</dt>
  <dd class="It-tag">Regular variables and functions are quite easy to pass:
      just pass in a reference to an existing or anonymous variable or function:
    <div style="height: 1.00em;">&#x00A0;</div>
    <pre>
    func( \$some_scalar );
    func( \@some_array  );
    func( [ 1 .. 10 ]   );
    func( \%some_hash   );
    func( { this =&gt; 10, that =&gt; 20 }   );
    func( \&amp;some_func   );
    func( sub { $_[0] ** $_[1] }   );
    </pre>
  </dd>
</dl>
<dl class="Bl-tag">
  <dt class="It-tag">Passing Filehandles</dt>
  <dd class="It-tag">As of Perl 5.6, you can represent filehandles with scalar
      variables which you treat as any other scalar.
    <div style="height: 1.00em;">&#x00A0;</div>
    <pre>
    open my $fh, $filename or die &quot;Cannot open $filename! $!&quot;;
    func( $fh );
    sub func {
        my $passed_fh = shift;
        my $line = &lt;$passed_fh&gt;;
    }
    </pre>
    <div style="height: 1.00em;">&#x00A0;</div>
    Before Perl 5.6, you had to use the *FH or &quot;\*FH&quot; notations. These
      are &quot;typeglobs&quot;--see &quot;Typeglobs and Filehandles&quot; in
      perldata and especially &quot;Pass by Reference&quot; in perlsub for more
      information.</dd>
</dl>
<dl class="Bl-tag">
  <dt class="It-tag">Passing Regexes</dt>
  <dd class="It-tag">Here's an example of how to pass in a string and a regular
      expression for it to match against. You construct the pattern with the
      &quot;qr//&quot; operator:
    <div style="height: 1.00em;">&#x00A0;</div>
    <pre>
    sub compare($$) {
        my ($val1, $regex) = @_;
        my $retval = $val1 =~ /$regex/;
        return $retval;
    }
    $match = compare(&quot;old McDonald&quot;, qr/d.*D/i);
    </pre>
  </dd>
</dl>
<dl class="Bl-tag">
  <dt class="It-tag">Passing Methods</dt>
  <dd class="It-tag">To pass an object method into a subroutine, you can do
      this:
    <div style="height: 1.00em;">&#x00A0;</div>
    <pre>
    call_a_lot(10, $some_obj, &quot;methname&quot;)
    sub call_a_lot {
        my ($count, $widget, $trick) = @_;
        for (my $i = 0; $i &lt; $count; $i++) {
            $widget-&gt;$trick();
        }
    }
    </pre>
    <div style="height: 1.00em;">&#x00A0;</div>
    Or, you can use a closure to bundle up the object, its method call, and
      arguments:
    <div style="height: 1.00em;">&#x00A0;</div>
    <pre>
    my $whatnot = sub { $some_obj-&gt;obfuscate(@args) };
    func($whatnot);
    sub func {
        my $code = shift;
        &amp;$code();
    }
    </pre>
    <div style="height: 1.00em;">&#x00A0;</div>
    You could also investigate the <i>can()</i> method in the UNIVERSAL class
      (part of the standard perl distribution).</dd>
</dl>
<h2 class="Ss" title="Ss" id="How_do_I_create_a_static_variable?"><a class="selflink" href="#How_do_I_create_a_static_variable?">How
  do I create a static variable?</a></h2>
(contributed by brian d foy)
<div class="Pp"></div>
In Perl 5.10, declare the variable with &quot;state&quot;. The &quot;state&quot;
  declaration creates the lexical variable that persists between calls to the
  subroutine:
<div class="Pp"></div>
<pre>
    sub counter { state $count = 1; $count++ }
</pre>
<div class="Pp"></div>
You can fake a static variable by using a lexical variable which goes out of
  scope. In this example, you define the subroutine &quot;counter&quot;, and it
  uses the lexical variable $count. Since you wrap this in a BEGIN block, $count
  is defined at compile-time, but also goes out of scope at the end of the BEGIN
  block. The BEGIN block also ensures that the subroutine and the value it uses
  is defined at compile-time so the subroutine is ready to use just like any
  other subroutine, and you can put this code in the same place as other
  subroutines in the program text (i.e. at the end of the code, typically). The
  subroutine &quot;counter&quot; still has a reference to the data, and is the
  only way you can access the value (and each time you do, you increment the
  value). The data in chunk of memory defined by $count is private to
  &quot;counter&quot;.
<div class="Pp"></div>
<pre>
    BEGIN {
        my $count = 1;
        sub counter { $count++ }
    }
    my $start = counter();
    .... # code that calls counter();
    my $end = counter();
</pre>
<div class="Pp"></div>
In the previous example, you created a function-private variable because only
  one function remembered its reference. You could define multiple functions
  while the variable is in scope, and each function can share the
  &quot;private&quot; variable. It's not really &quot;static&quot; because you
  can access it outside the function while the lexical variable is in scope, and
  even create references to it. In this example, &quot;increment_count&quot; and
  &quot;return_count&quot; share the variable. One function adds to the value
  and the other simply returns the value. They can both access $count, and since
  it has gone out of scope, there is no other way to access it.
<div class="Pp"></div>
<pre>
    BEGIN {
        my $count = 1;
        sub increment_count { $count++ }
        sub return_count    { $count }
    }
</pre>
<div class="Pp"></div>
To declare a file-private variable, you still use a lexical variable. A file is
  also a scope, so a lexical variable defined in the file cannot be seen from
  any other file.
<div class="Pp"></div>
See &quot;Persistent Private Variables&quot; in perlsub for more information.
  The discussion of closures in perlref may help you even though we did not use
  anonymous subroutines in this answer. See &quot;Persistent Private
  Variables&quot; in perlsub for details.
<h2 class="Ss" title="Ss" id="What's_the_difference_between_dynamic_and_lexical_(static)_scoping?_Between_local()_and_my()?"><a class="selflink" href="#What's_the_difference_between_dynamic_and_lexical_(static)_scoping?_Between_local()_and_my()?">What's
  the difference between dynamic and lexical (static) scoping? Between
  <i>local()</i> and <i>my()</i>?</a></h2>
&quot;local($x)&quot; saves away the old value of the global variable $x and
  assigns a new value for the duration of the subroutine <i>which is</i>
  <i>visible in other functions called from that subroutine</i>. This is done at
  run-time, so is called dynamic scoping. <i>local()</i> always affects global
  variables, also called package variables or dynamic variables.
<div class="Pp"></div>
&quot;my($x)&quot; creates a new variable that is only visible in the current
  subroutine. This is done at compile-time, so it is called lexical or static
  scoping. <i>my()</i> always affects private variables, also called lexical
  variables or (improperly) static(ly scoped) variables.
<div class="Pp"></div>
For instance:
<div class="Pp"></div>
<pre>
    sub visible {
        print &quot;var has value $var\n&quot;;
    }
    sub dynamic {
        local $var = 'local';    # new temporary value for the still-global
        visible();              #   variable called $var
    }
    sub lexical {
        my $var = 'private';    # new private variable, $var
        visible();              # (invisible outside of sub scope)
    }
    $var = 'global';
    visible();              # prints global
    dynamic();              # prints local
    lexical();              # prints global
</pre>
<div class="Pp"></div>
Notice how at no point does the value &quot;private&quot; get printed. That's
  because $var only has that value within the block of the <i>lexical()</i>
  function, and it is hidden from the called subroutine.
<div class="Pp"></div>
In summary, <i>local()</i> doesn't make what you think of as private, local
  variables. It gives a global variable a temporary value. <i>my()</i> is what
  you're looking for if you want private variables.
<div class="Pp"></div>
See &quot;Private Variables via <i>my()</i>&quot; in perlsub and &quot;Temporary
  Values via <i>local()</i>&quot; in perlsub for excruciating details.
<h2 class="Ss" title="Ss" id="How_can_I_access_a_dynamic_variable_while_a_similarly_named_lexical_is_in_scope?"><a class="selflink" href="#How_can_I_access_a_dynamic_variable_while_a_similarly_named_lexical_is_in_scope?">How
  can I access a dynamic variable while a similarly named lexical is in
  scope?</a></h2>
If you know your package, you can just mention it explicitly, as in
  $Some_Pack::var. Note that the notation $::var is <b>not</b> the dynamic $var
  in the current package, but rather the one in the &quot;main&quot; package, as
  though you had written $main::var.
<div class="Pp"></div>
<pre>
    use vars '$var';
    local $var = &quot;global&quot;;
    my    $var = &quot;lexical&quot;;
    print &quot;lexical is $var\n&quot;;
    print &quot;global  is $main::var\n&quot;;
</pre>
<div class="Pp"></div>
Alternatively you can use the compiler directive <i>our()</i> to bring a dynamic
  variable into the current lexical scope.
<div class="Pp"></div>
<pre>
    require 5.006; # our() did not exist before 5.6
    use vars '$var';
    local $var = &quot;global&quot;;
    my $var    = &quot;lexical&quot;;
    print &quot;lexical is $var\n&quot;;
    {
        our $var;
        print &quot;global  is $var\n&quot;;
    }
</pre>
<h2 class="Ss" title="Ss" id="What's_the_difference_between_deep_and_shallow_binding?"><a class="selflink" href="#What's_the_difference_between_deep_and_shallow_binding?">What's
  the difference between deep and shallow binding?</a></h2>
In deep binding, lexical variables mentioned in anonymous subroutines are the
  same ones that were in scope when the subroutine was created. In shallow
  binding, they are whichever variables with the same names happen to be in
  scope when the subroutine is called. Perl always uses deep binding of lexical
  variables (i.e., those created with <i>my()</i>). However, dynamic variables
  (aka global, local, or package variables) are effectively shallowly bound.
  Consider this just one more reason not to use them. See the answer to
  &quot;What's a closure?&quot;.
<h2 class="Ss" title="Ss" id="Why_doesn't_&quot;my($foo)_=_&lt;$fh&gt;;&quot;_work_right?"><a class="selflink" href="#Why_doesn't_&quot;my($foo)_=_&lt;$fh&gt;;&quot;_work_right?">Why
  doesn't &quot;my($foo) = &lt;$fh&gt;;&quot; work right?</a></h2>
&quot;my()&quot; and &quot;local()&quot; give list context to the right hand
  side of &quot;=&quot;. The &lt;$fh&gt; read operation, like so many of Perl's
  functions and operators, can tell which context it was called in and behaves
  appropriately. In general, the <i>scalar()</i> function can help. This
  function does nothing to the data itself (contrary to popular myth) but rather
  tells its argument to behave in whatever its scalar fashion is. If that
  function doesn't have a defined scalar behavior, this of course doesn't help
  you (such as with <i>sort()</i>).
<div class="Pp"></div>
To enforce scalar context in this particular case, however, you need merely omit
  the parentheses:
<div class="Pp"></div>
<pre>
    local($foo) = &lt;$fh&gt;;        # WRONG
    local($foo) = scalar(&lt;$fh&gt;);   # ok
    local $foo  = &lt;$fh&gt;;        # right
</pre>
<div class="Pp"></div>
You should probably be using lexical variables anyway, although the issue is the
  same here:
<div class="Pp"></div>
<pre>
    my($foo) = &lt;$fh&gt;;    # WRONG
    my $foo  = &lt;$fh&gt;;    # right
</pre>
<h2 class="Ss" title="Ss" id="How_do_I_redefine_a_builtin_function,_operator,_or_method?"><a class="selflink" href="#How_do_I_redefine_a_builtin_function,_operator,_or_method?">How
  do I redefine a builtin function, operator, or method?</a></h2>
Why do you want to do that? :-)
<div class="Pp"></div>
If you want to override a predefined function, such as <i>open()</i>, then
  you'll have to import the new definition from a different module. See
  &quot;Overriding Built-in Functions&quot; in perlsub.
<div class="Pp"></div>
If you want to overload a Perl operator, such as &quot;+&quot; or
  &quot;**&quot;, then you'll want to use the &quot;use overload&quot; pragma,
  documented in overload.
<div class="Pp"></div>
If you're talking about obscuring method calls in parent classes, see
  &quot;Overridden Methods&quot; in perltoot.
<h2 class="Ss" title="Ss" id="What's_the_difference_between_calling_a_function_as_&amp;foo_and_foo()?"><a class="selflink" href="#What's_the_difference_between_calling_a_function_as_&amp;foo_and_foo()?">What's
  the difference between calling a function as &amp;foo and
  <i>foo()</i>?</a></h2>
(contributed by brian d foy)
<div class="Pp"></div>
Calling a subroutine as &amp;foo with no trailing parentheses ignores the
  prototype of &quot;foo&quot; and passes it the current value of the argument
  list, @_. Here's an example; the &quot;bar&quot; subroutine calls &amp;foo,
  which prints its arguments list:
<div class="Pp"></div>
<pre>
    sub bar { &amp;foo }
    sub foo { print &quot;Args in foo are: @_\n&quot; }
    bar( qw( a b c ) );
</pre>
<div class="Pp"></div>
When you call &quot;bar&quot; with arguments, you see that &quot;foo&quot; got
  the same @_:
<div class="Pp"></div>
<pre>
    Args in foo are: a b c
</pre>
<div class="Pp"></div>
Calling the subroutine with trailing parentheses, with or without arguments,
  does not use the current @_ and respects the subroutine prototype. Changing
  the example to put parentheses after the call to &quot;foo&quot; changes the
  program:
<div class="Pp"></div>
<pre>
    sub bar { &amp;foo() }
    sub foo { print &quot;Args in foo are: @_\n&quot; }
    bar( qw( a b c ) );
</pre>
<div class="Pp"></div>
Now the output shows that &quot;foo&quot; doesn't get the @_ from its caller.
<div class="Pp"></div>
<pre>
    Args in foo are:
</pre>
<div class="Pp"></div>
The main use of the @_ pass-through feature is to write subroutines whose main
  job it is to call other subroutines for you. For further details, see perlsub.
<h2 class="Ss" title="Ss" id="How_do_I_create_a_switch_or_case_statement?"><a class="selflink" href="#How_do_I_create_a_switch_or_case_statement?">How
  do I create a switch or case statement?</a></h2>
In Perl 5.10, use the &quot;given-when&quot; construct described in perlsyn:
<div class="Pp"></div>
<pre>
    use 5.010;
    given ( $string ) {
        when( 'Fred' )        { say &quot;I found Fred!&quot; }
        when( 'Barney' )      { say &quot;I found Barney!&quot; }
        when( /Bamm-?Bamm/ )  { say &quot;I found Bamm-Bamm!&quot; }
        default               { say &quot;I don't recognize the name!&quot; }
    };
</pre>
<div class="Pp"></div>
If one wants to use pure Perl and to be compatible with Perl versions prior to
  5.10, the general answer is to use &quot;if-elsif-else&quot;:
<div class="Pp"></div>
<pre>
    for ($variable_to_test) {
        if    (/pat1/)  { }     # do something
        elsif (/pat2/)  { }     # do something else
        elsif (/pat3/)  { }     # do something else
        else            { }     # default
    }
</pre>
<div class="Pp"></div>
Here's a simple example of a switch based on pattern matching, lined up in a way
  to make it look more like a switch statement. We'll do a multiway conditional
  based on the type of reference stored in $whatchamacallit:
<div class="Pp"></div>
<pre>
    SWITCH: for (ref $whatchamacallit) {
        /^$/           &amp;&amp; die &quot;not a reference&quot;;
        /SCALAR/       &amp;&amp; do {
                        print_scalar($$ref);
                        last SWITCH;
                      };
        /ARRAY/        &amp;&amp; do {
                        print_array(@$ref);
                        last SWITCH;
                      };
        /HASH/        &amp;&amp; do {
                        print_hash(%$ref);
                        last SWITCH;
                      };
        /CODE/        &amp;&amp; do {
                        warn &quot;can't print function ref&quot;;
                        last SWITCH;
                      };
        # DEFAULT
        warn &quot;User defined type skipped&quot;;
    }
</pre>
<div class="Pp"></div>
See perlsyn for other examples in this style.
<div class="Pp"></div>
Sometimes you should change the positions of the constant and the variable. For
  example, let's say you wanted to test which of many answers you were given,
  but in a case-insensitive way that also allows abbreviations. You can use the
  following technique if the strings all start with different characters or if
  you want to arrange the matches so that one takes precedence over another, as
  &quot;SEND&quot; has precedence over &quot;STOP&quot; here:
<div class="Pp"></div>
<pre>
    chomp($answer = &lt;&gt;);
    if    (&quot;SEND&quot;  =~ /^\Q$answer/i) { print &quot;Action is send\n&quot;  }
    elsif (&quot;STOP&quot;  =~ /^\Q$answer/i) { print &quot;Action is stop\n&quot;  }
    elsif (&quot;ABORT&quot; =~ /^\Q$answer/i) { print &quot;Action is abort\n&quot; }
    elsif (&quot;LIST&quot;  =~ /^\Q$answer/i) { print &quot;Action is list\n&quot;  }
    elsif (&quot;EDIT&quot;  =~ /^\Q$answer/i) { print &quot;Action is edit\n&quot;  }
</pre>
<div class="Pp"></div>
A totally different approach is to create a hash of function references.
<div class="Pp"></div>
<pre>
    my %commands = (
        &quot;happy&quot; =&gt; \&amp;joy,
        &quot;sad&quot;,  =&gt; \&amp;sullen,
        &quot;done&quot;  =&gt; sub { die &quot;See ya!&quot; },
        &quot;mad&quot;   =&gt; \&amp;angry,
    );
    print &quot;How are you? &quot;;
    chomp($string = &lt;STDIN&gt;);
    if ($commands{$string}) {
        $commands{$string}-&gt;();
    } else {
        print &quot;No such command: $string\n&quot;;
    }
</pre>
<div class="Pp"></div>
Starting from Perl 5.8, a source filter module, &quot;Switch&quot;, can also be
  used to get switch and case. Its use is now discouraged, because it's not
  fully compatible with the native switch of Perl 5.10, and because, as it's
  implemented as a source filter, it doesn't always work as intended when
  complex syntax is involved.
<h2 class="Ss" title="Ss" id="How_can_I_catch_accesses_to_undefined_variables,_functions,_or_methods?"><a class="selflink" href="#How_can_I_catch_accesses_to_undefined_variables,_functions,_or_methods?">How
  can I catch accesses to undefined variables, functions, or methods?</a></h2>
The AUTOLOAD method, discussed in &quot;Autoloading&quot; in perlsub and
  &quot;AUTOLOAD: Proxy Methods&quot; in perltoot, lets you capture calls to
  undefined functions and methods.
<div class="Pp"></div>
When it comes to undefined variables that would trigger a warning under
  &quot;use warnings&quot;, you can promote the warning to an error.
<div class="Pp"></div>
<pre>
    use warnings FATAL =&gt; qw(uninitialized);
</pre>
<h2 class="Ss" title="Ss" id="Why_can't_a_method_included_in_this_same_file_be_found?"><a class="selflink" href="#Why_can't_a_method_included_in_this_same_file_be_found?">Why
  can't a method included in this same file be found?</a></h2>
Some possible reasons: your inheritance is getting confused, you've misspelled
  the method name, or the object is of the wrong type. Check out perltoot for
  details about any of the above cases. You may also use &quot;print
  ref($object)&quot; to find out the class $object was blessed into.
<div class="Pp"></div>
Another possible reason for problems is that you've used the indirect object
  syntax (eg, &quot;find Guru &quot;Samy&quot;&quot;) on a class name before
  Perl has seen that such a package exists. It's wisest to make sure your
  packages are all defined before you start using them, which will be taken care
  of if you use the &quot;use&quot; statement instead of &quot;require&quot;. If
  not, make sure to use arrow notation (eg.,
  &quot;Guru-&gt;find(&quot;Samy&quot;)&quot;) instead. Object notation is
  explained in perlobj.
<div class="Pp"></div>
Make sure to read about creating modules in perlmod and the perils of indirect
  objects in &quot;Method Invocation&quot; in perlobj.
<h2 class="Ss" title="Ss" id="How_can_I_find_out_my_current_or_calling_package?"><a class="selflink" href="#How_can_I_find_out_my_current_or_calling_package?">How
  can I find out my current or calling package?</a></h2>
(contributed by brian d foy)
<div class="Pp"></div>
To find the package you are currently in, use the special literal
  &quot;__PACKAGE__&quot;, as documented in perldata. You can only use the
  special literals as separate tokens, so you can't interpolate them into
  strings like you can with variables:
<div class="Pp"></div>
<pre>
    my $current_package = __PACKAGE__;
    print &quot;I am in package $current_package\n&quot;;
</pre>
<div class="Pp"></div>
If you want to find the package calling your code, perhaps to give better
  diagnostics as Carp does, use the &quot;caller&quot; built-in:
<div class="Pp"></div>
<pre>
    sub foo {
        my @args = ...;
        my( $package, $filename, $line ) = caller;
        print &quot;I was called from package $package\n&quot;;
        );
</pre>
<div class="Pp"></div>
By default, your program starts in package &quot;main&quot;, so you will always
  be in some package.
<div class="Pp"></div>
This is different from finding out the package an object is blessed into, which
  might not be the current package. For that, use &quot;blessed&quot; from
  Scalar::Util, part of the Standard Library since Perl 5.8:
<div class="Pp"></div>
<pre>
    use Scalar::Util qw(blessed);
    my $object_package = blessed( $object );
</pre>
<div class="Pp"></div>
Most of the time, you shouldn't care what package an object is blessed into,
  however, as long as it claims to inherit from that class:
<div class="Pp"></div>
<pre>
    my $is_right_class = eval { $object-&gt;isa( $package ) }; # true or false
</pre>
<div class="Pp"></div>
And, with Perl 5.10 and later, you don't have to check for an inheritance to see
  if the object can handle a role. For that, you can use &quot;DOES&quot;, which
  comes from &quot;UNIVERSAL&quot;:
<div class="Pp"></div>
<pre>
    my $class_does_it = eval { $object-&gt;DOES( $role ) }; # true or false
</pre>
<div class="Pp"></div>
You can safely replace &quot;isa&quot; with &quot;DOES&quot; (although the
  converse is not true).
<h2 class="Ss" title="Ss" id="How_can_I_comment_out_a_large_block_of_Perl_code?"><a class="selflink" href="#How_can_I_comment_out_a_large_block_of_Perl_code?">How
  can I comment out a large block of Perl code?</a></h2>
(contributed by brian d foy)
<div class="Pp"></div>
The quick-and-dirty way to comment out more than one line of Perl is to surround
  those lines with Pod directives. You have to put these directives at the
  beginning of the line and somewhere where Perl expects a new statement (so not
  in the middle of statements like the &quot;#&quot; comments). You end the
  comment with &quot;=cut&quot;, ending the Pod section:
<div class="Pp"></div>
<pre>
    =pod
    my $object = NotGonnaHappen-&gt;new();
    ignored_sub();
    $wont_be_assigned = 37;
    =cut
</pre>
<div class="Pp"></div>
The quick-and-dirty method only works well when you don't plan to leave the
  commented code in the source. If a Pod parser comes along, you're multiline
  comment is going to show up in the Pod translation. A better way hides it from
  Pod parsers as well.
<div class="Pp"></div>
The &quot;=begin&quot; directive can mark a section for a particular purpose. If
  the Pod parser doesn't want to handle it, it just ignores it. Label the
  comments with &quot;comment&quot;. End the comment using &quot;=end&quot; with
  the same label. You still need the &quot;=cut&quot; to go back to Perl code
  from the Pod comment:
<div class="Pp"></div>
<pre>
    =begin comment
    my $object = NotGonnaHappen-&gt;new();
    ignored_sub();
    $wont_be_assigned = 37;
    =end comment
    =cut
</pre>
<div class="Pp"></div>
For more information on Pod, check out perlpod and perlpodspec.
<h2 class="Ss" title="Ss" id="How_do_I_clear_a_package?"><a class="selflink" href="#How_do_I_clear_a_package?">How
  do I clear a package?</a></h2>
Use this code, provided by Mark-Jason Dominus:
<div class="Pp"></div>
<pre>
    sub scrub_package {
        no strict 'refs';
        my $pack = shift;
        die &quot;Shouldn't delete main package&quot;
            if $pack eq &quot;&quot; || $pack eq &quot;main&quot;;
        my $stash = *{$pack . '::'}{HASH};
        my $name;
        foreach $name (keys %$stash) {
            my $fullname = $pack . '::' . $name;
            # Get rid of everything with that name.
            undef $$fullname;
            undef @$fullname;
            undef %$fullname;
            undef &amp;$fullname;
            undef *$fullname;
        }
    }
</pre>
<div class="Pp"></div>
Or, if you're using a recent release of Perl, you can just use the
  <i>Symbol::delete_package()</i> function instead.
<h2 class="Ss" title="Ss" id="How_can_I_use_a_variable_as_a_variable_name?"><a class="selflink" href="#How_can_I_use_a_variable_as_a_variable_name?">How
  can I use a variable as a variable name?</a></h2>
Beginners often think they want to have a variable contain the name of a
  variable.
<div class="Pp"></div>
<pre>
    $fred    = 23;
    $varname = &quot;fred&quot;;
    ++$$varname;         # $fred now 24
</pre>
<div class="Pp"></div>
This works <i>sometimes</i>, but it is a very bad idea for two reasons.
<div class="Pp"></div>
The first reason is that this technique <i>only works on global</i>
  <i>variables</i>. That means that if $fred is a lexical variable created with
  <i>my()</i> in the above example, the code wouldn't work at all: you'd
  accidentally access the global and skip right over the private lexical
  altogether. Global variables are bad because they can easily collide
  accidentally and in general make for non-scalable and confusing code.
<div class="Pp"></div>
Symbolic references are forbidden under the &quot;use strict&quot; pragma. They
  are not true references and consequently are not reference-counted or
  garbage-collected.
<div class="Pp"></div>
The other reason why using a variable to hold the name of another variable is a
  bad idea is that the question often stems from a lack of understanding of Perl
  data structures, particularly hashes. By using symbolic references, you are
  just using the package's symbol-table hash (like %main::) instead of a
  user-defined hash. The solution is to use your own hash or a real reference
  instead.
<div class="Pp"></div>
<pre>
    $USER_VARS{&quot;fred&quot;} = 23;
    my $varname = &quot;fred&quot;;
    $USER_VARS{$varname}++;  # not $$varname++
</pre>
<div class="Pp"></div>
There we're using the %USER_VARS hash instead of symbolic references. Sometimes
  this comes up in reading strings from the user with variable references and
  wanting to expand them to the values of your perl program's variables. This is
  also a bad idea because it conflates the program-addressable namespace and the
  user-addressable one. Instead of reading a string and expanding it to the
  actual contents of your program's own variables:
<div class="Pp"></div>
<pre>
    $str = 'this has a $fred and $barney in it';
    $str =~ s/(\$\w+)/$1/eeg;          # need double eval
</pre>
<div class="Pp"></div>
it would be better to keep a hash around like %USER_VARS and have variable
  references actually refer to entries in that hash:
<div class="Pp"></div>
<pre>
    $str =~ s/\$(\w+)/$USER_VARS{$1}/g;   # no /e here at all
</pre>
<div class="Pp"></div>
That's faster, cleaner, and safer than the previous approach. Of course, you
  don't need to use a dollar sign. You could use your own scheme to make it less
  confusing, like bracketed percent symbols, etc.
<div class="Pp"></div>
<pre>
    $str = 'this has a %fred% and %barney% in it';
    $str =~ s/%(\w+)%/$USER_VARS{$1}/g;   # no /e here at all
</pre>
<div class="Pp"></div>
Another reason that folks sometimes think they want a variable to contain the
  name of a variable is that they don't know how to build proper data structures
  using hashes. For example, let's say they wanted two hashes in their program:
  %fred and %barney, and that they wanted to use another scalar variable to
  refer to those by name.
<div class="Pp"></div>
<pre>
    $name = &quot;fred&quot;;
    $$name{WIFE} = &quot;wilma&quot;;     # set %fred
    $name = &quot;barney&quot;;
    $$name{WIFE} = &quot;betty&quot;;    # set %barney
</pre>
<div class="Pp"></div>
This is still a symbolic reference, and is still saddled with the problems
  enumerated above. It would be far better to write:
<div class="Pp"></div>
<pre>
    $folks{&quot;fred&quot;}{WIFE}   = &quot;wilma&quot;;
    $folks{&quot;barney&quot;}{WIFE} = &quot;betty&quot;;
</pre>
<div class="Pp"></div>
And just use a multilevel hash to start with.
<div class="Pp"></div>
The only times that you absolutely <i>must</i> use symbolic references are when
  you really must refer to the symbol table. This may be because it's something
  that one can't take a real reference to, such as a format name. Doing so may
  also be important for method calls, since these always go through the symbol
  table for resolution.
<div class="Pp"></div>
In those cases, you would turn off &quot;strict 'refs'&quot; temporarily so you
  can play around with the symbol table. For example:
<div class="Pp"></div>
<pre>
    @colors = qw(red blue green yellow orange purple violet);
    for my $name (@colors) {
        no strict 'refs';  # renege for the block
        *$name = sub { &quot;&lt;FONT COLOR='$name'&gt;@_&lt;/FONT&gt;&quot; };
    }
</pre>
<div class="Pp"></div>
All those functions ( <i>red()</i>, <i>blue()</i>, <i>green()</i>, etc.) appear
  to be separate, but the real code in the closure actually was compiled only
  once.
<div class="Pp"></div>
So, sometimes you might want to use symbolic references to manipulate the symbol
  table directly. This doesn't matter for formats, handles, and subroutines,
  because they are always global--you can't use <i>my()</i> on them. For
  scalars, arrays, and hashes, though--and usually for subroutines-- you
  probably only want to use hard references.
<h2 class="Ss" title="Ss" id="What_does_&quot;bad_interpreter&quot;_mean?"><a class="selflink" href="#What_does_&quot;bad_interpreter&quot;_mean?">What
  does &quot;bad interpreter&quot; mean?</a></h2>
(contributed by brian d foy)
<div class="Pp"></div>
The &quot;bad interpreter&quot; message comes from the shell, not perl. The
  actual message may vary depending on your platform, shell, and locale
  settings.
<div class="Pp"></div>
If you see &quot;bad interpreter - no such file or directory&quot;, the first
  line in your perl script (the &quot;shebang&quot; line) does not contain the
  right path to perl (or any other program capable of running scripts).
  Sometimes this happens when you move the script from one machine to another
  and each machine has a different path to perl--/usr/bin/perl versus
  /usr/local/bin/perl for instance. It may also indicate that the source machine
  has CRLF line terminators and the destination machine has LF only: the shell
  tries to find /usr/bin/perl&lt;CR&gt;, but can't.
<div class="Pp"></div>
If you see &quot;bad interpreter: Permission denied&quot;, you need to make your
  script executable.
<div class="Pp"></div>
In either case, you should still be able to run the scripts with perl
  explicitly:
<div class="Pp"></div>
<pre>
    % perl script.pl
</pre>
<div class="Pp"></div>
If you get a message like &quot;perl: command not found&quot;, perl is not in
  your PATH, which might also mean that the location of perl is not where you
  expect it so you need to adjust your shebang line.
<h1 class="Sh" title="Sh" id="AUTHOR_AND_COPYRIGHT"><a class="selflink" href="#AUTHOR_AND_COPYRIGHT">AUTHOR
  AND COPYRIGHT</a></h1>
Copyright (c) 1997-2010 Tom Christiansen, Nathan Torkington, and other authors
  as noted. All rights reserved.
<div class="Pp"></div>
This documentation is free; you can redistribute it and/or modify it under the
  same terms as Perl itself.
<div class="Pp"></div>
Irrespective of its distribution, all code examples in this file are hereby
  placed into the public domain. You are permitted and encouraged to use this
  code in your own programs for fun or for profit as you see fit. A simple
  comment in the code giving credit would be courteous but is not
  required.</div>
<table class="foot">
  <tr>
    <td class="foot-date">2013-03-04</td>
    <td class="foot-os">perl v5.16.3</td>
  </tr>
</table>
</body>
</html>
