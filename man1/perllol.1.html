<!DOCTYPE html>
<html>
<head>
  <meta charset="utf-8"/>
  <style>
    table.head, table.foot { width: 100%; }
    td.head-rtitle, td.foot-os { text-align: right; }
    td.head-vol { text-align: center; }
    div.Pp { margin: 1ex 0ex; }
  </style>
  <title>PERLLOL(1)</title>
</head>
<body>
<table class="head">
  <tr>
    <td class="head-ltitle">PERLLOL(1)</td>
    <td class="head-vol">Perl Programmers Reference Guide</td>
    <td class="head-rtitle">PERLLOL(1)</td>
  </tr>
</table>
<div class="manual-text">
<h1 class="Sh" title="Sh" id="NAME"><a class="selflink" href="#NAME">NAME</a></h1>
perllol - Manipulating Arrays of Arrays in Perl
<h1 class="Sh" title="Sh" id="DESCRIPTION"><a class="selflink" href="#DESCRIPTION">DESCRIPTION</a></h1>
<h2 class="Ss" title="Ss" id="Declaration_and_Access_of_Arrays_of_Arrays"><a class="selflink" href="#Declaration_and_Access_of_Arrays_of_Arrays">Declaration
  and Access of Arrays of Arrays</a></h2>
The simplest two-level data structure to build in Perl is an array of arrays,
  sometimes casually called a list of lists. It's reasonably easy to understand,
  and almost everything that applies here will also be applicable later on with
  the fancier data structures.
<div class="Pp"></div>
An array of an array is just a regular old array @AoA that you can get at with
  two subscripts, like $AoA[3][2]. Here's a declaration of the array:
<div class="Pp"></div>
<pre>
    use 5.010;  # so we can use say()
    # assign to our array, an array of array references
    @AoA = (
           [ &quot;fred&quot;, &quot;barney&quot;, &quot;pebbles&quot;, &quot;bambam&quot;, &quot;dino&quot;, ],
           [ &quot;george&quot;, &quot;jane&quot;, &quot;elroy&quot;, &quot;judy&quot;, ],
           [ &quot;homer&quot;, &quot;bart&quot;, &quot;marge&quot;, &quot;maggie&quot;, ],
    );
    say $AoA[2][1];
  bart
</pre>
<div class="Pp"></div>
Now you should be very careful that the outer bracket type is a round one, that
  is, a parenthesis. That's because you're assigning to an @array, so you need
  parentheses. If you wanted there <i>not</i> to be an @AoA, but rather just a
  reference to it, you could do something more like this:
<div class="Pp"></div>
<pre>
    # assign a reference to array of array references
    $ref_to_AoA = [
        [ &quot;fred&quot;, &quot;barney&quot;, &quot;pebbles&quot;, &quot;bambam&quot;, &quot;dino&quot;, ],
        [ &quot;george&quot;, &quot;jane&quot;, &quot;elroy&quot;, &quot;judy&quot;, ],
        [ &quot;homer&quot;, &quot;bart&quot;, &quot;marge&quot;, &quot;maggie&quot;, ],
    ];
    say $ref_to_AoA-&gt;[2][1];
  bart
</pre>
<div class="Pp"></div>
Notice that the outer bracket type has changed, and so our access syntax has
  also changed. That's because unlike C, in perl you can't freely interchange
  arrays and references thereto. $ref_to_AoA is a reference to an array, whereas
  @AoA is an array proper. Likewise, $AoA[2] is not an array, but an array ref.
  So how come you can write these:
<div class="Pp"></div>
<pre>
    $AoA[2][2]
    $ref_to_AoA-&gt;[2][2]
</pre>
<div class="Pp"></div>
instead of having to write these:
<div class="Pp"></div>
<pre>
    $AoA[2]-&gt;[2]
    $ref_to_AoA-&gt;[2]-&gt;[2]
</pre>
<div class="Pp"></div>
Well, that's because the rule is that on adjacent brackets only (whether square
  or curly), you are free to omit the pointer dereferencing arrow. But you
  cannot do so for the very first one if it's a scalar containing a reference,
  which means that $ref_to_AoA always needs it.
<h2 class="Ss" title="Ss" id="Growing_Your_Own"><a class="selflink" href="#Growing_Your_Own">Growing
  Your Own</a></h2>
That's all well and good for declaration of a fixed data structure, but what if
  you wanted to add new elements on the fly, or build it up entirely from
  scratch?
<div class="Pp"></div>
First, let's look at reading it in from a file. This is something like adding a
  row at a time. We'll assume that there's a flat file in which each line is a
  row and each word an element. If you're trying to develop an @AoA array
  containing all these, here's the right way to do that:
<div class="Pp"></div>
<pre>
    while (&lt;&gt;) {
        @tmp = split;
        push @AoA, [ @tmp ];
    }
</pre>
<div class="Pp"></div>
You might also have loaded that from a function:
<div class="Pp"></div>
<pre>
    for $i ( 1 .. 10 ) {
        $AoA[$i] = [ somefunc($i) ];
    }
</pre>
<div class="Pp"></div>
Or you might have had a temporary variable sitting around with the array in it.
<div class="Pp"></div>
<pre>
    for $i ( 1 .. 10 ) {
        @tmp = somefunc($i);
        $AoA[$i] = [ @tmp ];
    }
</pre>
<div class="Pp"></div>
It's important you make sure to use the &quot;[ ]&quot; array reference
  constructor. That's because this wouldn't work:
<div class="Pp"></div>
<pre>
    $AoA[$i] = @tmp;   # WRONG!
</pre>
<div class="Pp"></div>
The reason that doesn't do what you want is because assigning a named array like
  that to a scalar is taking an array in scalar context, which means just counts
  the number of elements in @tmp.
<div class="Pp"></div>
If you are running under &quot;use strict&quot; (and if you aren't, why in the
  world aren't you?), you'll have to add some declarations to make it happy:
<div class="Pp"></div>
<pre>
    use strict;
    my(@AoA, @tmp);
    while (&lt;&gt;) {
        @tmp = split;
        push @AoA, [ @tmp ];
    }
</pre>
<div class="Pp"></div>
Of course, you don't need the temporary array to have a name at all:
<div class="Pp"></div>
<pre>
    while (&lt;&gt;) {
        push @AoA, [ split ];
    }
</pre>
<div class="Pp"></div>
You also don't have to use <i>push()</i>. You could just make a direct
  assignment if you knew where you wanted to put it:
<div class="Pp"></div>
<pre>
    my (@AoA, $i, $line);
    for $i ( 0 .. 10 ) {
        $line = &lt;&gt;;
        $AoA[$i] = [ split &quot; &quot;, $line ];
    }
</pre>
<div class="Pp"></div>
or even just
<div class="Pp"></div>
<pre>
    my (@AoA, $i);
    for $i ( 0 .. 10 ) {
        $AoA[$i] = [ split &quot; &quot;, &lt;&gt; ];
    }
</pre>
<div class="Pp"></div>
You should in general be leery of using functions that could potentially return
  lists in scalar context without explicitly stating such. This would be clearer
  to the casual reader:
<div class="Pp"></div>
<pre>
    my (@AoA, $i);
    for $i ( 0 .. 10 ) {
        $AoA[$i] = [ split &quot; &quot;, scalar(&lt;&gt;) ];
    }
</pre>
<div class="Pp"></div>
If you wanted to have a $ref_to_AoA variable as a reference to an array, you'd
  have to do something like this:
<div class="Pp"></div>
<pre>
    while (&lt;&gt;) {
        push @$ref_to_AoA, [ split ];
    }
</pre>
<div class="Pp"></div>
Now you can add new rows. What about adding new columns? If you're dealing with
  just matrices, it's often easiest to use simple assignment:
<div class="Pp"></div>
<pre>
    for $x (1 .. 10) {
        for $y (1 .. 10) {
            $AoA[$x][$y] = func($x, $y);
        }
    }
    for $x ( 3, 7, 9 ) {
        $AoA[$x][20] += func2($x);
    }
</pre>
<div class="Pp"></div>
It doesn't matter whether those elements are already there or not: it'll gladly
  create them for you, setting intervening elements to &quot;undef&quot; as need
  be.
<div class="Pp"></div>
If you wanted just to append to a row, you'd have to do something a bit funnier
  looking:
<div class="Pp"></div>
<pre>
    # add new columns to an existing row
    push @{ $AoA[0] }, &quot;wilma&quot;, &quot;betty&quot;;   # explicit deref
</pre>
<div class="Pp"></div>
Prior to Perl 5.14, this wouldn't even compile:
<div class="Pp"></div>
<pre>
    push $AoA[0], &quot;wilma&quot;, &quot;betty&quot;;        # implicit deref
</pre>
<div class="Pp"></div>
How come? Because once upon a time, the argument to <i>push()</i> had to be a
  real array, not just a reference to one. That's no longer true. In fact, the
  line marked &quot;implicit deref&quot; above works just fine--in this
  instance--to do what the one that says explicit deref did.
<div class="Pp"></div>
The reason I said &quot;in this instance&quot; is because that <i>only</i> works
  because $AoA[0] already held an array reference. If you try that on an
  undefined variable, you'll take an exception. That's because the implicit
  derefererence will never autovivify an undefined variable the way &quot;@{
  }&quot; always will:
<div class="Pp"></div>
<pre>
    my $aref = undef;
    push $aref,  qw(some more values);  # WRONG!
    push @$aref, qw(a few more);        # ok
</pre>
<div class="Pp"></div>
If you want to take advantage of this new implicit dereferencing behavior, go
  right ahead: it makes code easier on the eye and wrist. Just understand that
  older releases will choke on it during compilation. Whenever you make use of
  something that works only in some given release of Perl and later, but not
  earlier, you should place a prominent
<div class="Pp"></div>
<pre>
    use v5.14;   # needed for implicit deref of array refs by array ops
</pre>
<div class="Pp"></div>
directive at the top of the file that needs it. That way when somebody tries to
  run the new code under an old perl, rather than getting an error like
<div class="Pp"></div>
<pre>
    Type of arg 1 to push must be array (not array element) at /tmp/a line 8, near &quot;&quot;betty&quot;;&quot;
    Execution of /tmp/a aborted due to compilation errors.
</pre>
<div class="Pp"></div>
they'll be politely informed that
<div class="Pp"></div>
<pre>
    Perl v5.14.0 required--this is only v5.12.3, stopped at /tmp/a line 1.
    BEGIN failed--compilation aborted at /tmp/a line 1.
</pre>
<h2 class="Ss" title="Ss" id="Access_and_Printing"><a class="selflink" href="#Access_and_Printing">Access
  and Printing</a></h2>
Now it's time to print your data structure out. How are you going to do that?
  Well, if you want only one of the elements, it's trivial:
<div class="Pp"></div>
<pre>
    print $AoA[0][0];
</pre>
<div class="Pp"></div>
If you want to print the whole thing, though, you can't say
<div class="Pp"></div>
<pre>
    print @AoA;         # WRONG
</pre>
<div class="Pp"></div>
because you'll get just references listed, and perl will never automatically
  dereference things for you. Instead, you have to roll yourself a loop or two.
  This prints the whole structure, using the shell-style <i>for()</i> construct
  to loop across the outer set of subscripts.
<div class="Pp"></div>
<pre>
    for $aref ( @AoA ) {
        say &quot;\t [ @$aref ],&quot;;
    }
</pre>
<div class="Pp"></div>
If you wanted to keep track of subscripts, you might do this:
<div class="Pp"></div>
<pre>
    for $i ( 0 .. $#AoA ) {
        say &quot;\t elt $i is [ @{$AoA[$i]} ],&quot;;
    }
</pre>
<div class="Pp"></div>
or maybe even this. Notice the inner loop.
<div class="Pp"></div>
<pre>
    for $i ( 0 .. $#AoA ) {
        for $j ( 0 .. $#{$AoA[$i]} ) {
            say &quot;elt $i $j is $AoA[$i][$j]&quot;;
        }
    }
</pre>
<div class="Pp"></div>
As you can see, it's getting a bit complicated. That's why sometimes is easier
  to take a temporary on your way through:
<div class="Pp"></div>
<pre>
    for $i ( 0 .. $#AoA ) {
        $aref = $AoA[$i];
        for $j ( 0 .. $#{$aref} ) {
            say &quot;elt $i $j is $AoA[$i][$j]&quot;;
        }
    }
</pre>
<div class="Pp"></div>
Hmm... that's still a bit ugly. How about this:
<div class="Pp"></div>
<pre>
    for $i ( 0 .. $#AoA ) {
        $aref = $AoA[$i];
        $n = @$aref - 1;
        for $j ( 0 .. $n ) {
            say &quot;elt $i $j is $AoA[$i][$j]&quot;;
        }
    }
</pre>
<div class="Pp"></div>
When you get tired of writing a custom print for your data structures, you might
  look at the standard Dumpvalue or Data::Dumper modules. The former is what the
  Perl debugger uses, while the latter generates parsable Perl code. For
  example:
<div class="Pp"></div>
<pre>
    use v5.14;     # using the + prototype, new to v5.14
    sub show(+) {
        require Dumpvalue;
        state $prettily = new Dumpvalue::
                            tick        =&gt; q(&quot;),
                            compactDump =&gt; 1,  # comment these two lines out
                            veryCompact =&gt; 1,  # if you want a bigger dump
                        ;
        dumpValue $prettily @_;
    }
    # Assign a list of array references to an array.
    my @AoA = (
           [ &quot;fred&quot;, &quot;barney&quot; ],
           [ &quot;george&quot;, &quot;jane&quot;, &quot;elroy&quot; ],
           [ &quot;homer&quot;, &quot;marge&quot;, &quot;bart&quot; ],
    );
    push $AoA[0], &quot;wilma&quot;, &quot;betty&quot;;
    show @AoA;
</pre>
<div class="Pp"></div>
will print out:
<div class="Pp"></div>
<pre>
    0  0..3  &quot;fred&quot; &quot;barney&quot; &quot;wilma&quot; &quot;betty&quot;
    1  0..2  &quot;george&quot; &quot;jane&quot; &quot;elroy&quot;
    2  0..2  &quot;homer&quot; &quot;marge&quot; &quot;bart&quot;
</pre>
<div class="Pp"></div>
Whereas if you comment out the two lines I said you might wish to, then it shows
  it to you this way instead:
<div class="Pp"></div>
<pre>
    0  ARRAY(0x8031d0)
       0  &quot;fred&quot;
       1  &quot;barney&quot;
       2  &quot;wilma&quot;
       3  &quot;betty&quot;
    1  ARRAY(0x803d40)
       0  &quot;george&quot;
       1  &quot;jane&quot;
       2  &quot;elroy&quot;
    2  ARRAY(0x803e10)
       0  &quot;homer&quot;
       1  &quot;marge&quot;
       2  &quot;bart&quot;
</pre>
<h2 class="Ss" title="Ss" id="Slices"><a class="selflink" href="#Slices">Slices</a></h2>
If you want to get at a slice (part of a row) in a multidimensional array,
  you're going to have to do some fancy subscripting. That's because while we
  have a nice synonym for single elements via the pointer arrow for
  dereferencing, no such convenience exists for slices.
<div class="Pp"></div>
Here's how to do one operation using a loop. We'll assume an @AoA variable as
  before.
<div class="Pp"></div>
<pre>
    @part = ();
    $x = 4;
    for ($y = 7; $y &lt; 13; $y++) {
        push @part, $AoA[$x][$y];
    }
</pre>
<div class="Pp"></div>
That same loop could be replaced with a slice operation:
<div class="Pp"></div>
<pre>
    @part = @{$AoA[4]}[7..12];
</pre>
<div class="Pp"></div>
or spaced out a bit:
<div class="Pp"></div>
<pre>
    @part = @{ $AoA[4] } [ 7..12 ];
</pre>
<div class="Pp"></div>
But as you might well imagine, this can get pretty rough on the reader.
<div class="Pp"></div>
Ah, but what if you wanted a <i>two-dimensional slice</i>, such as having $x run
  from 4..8 and $y run from 7 to 12? Hmm... here's the simple way:
<div class="Pp"></div>
<pre>
    @newAoA = ();
    for ($startx = $x = 4; $x &lt;= 8; $x++) {
        for ($starty = $y = 7; $y &lt;= 12; $y++) {
            $newAoA[$x - $startx][$y - $starty] = $AoA[$x][$y];
        }
    }
</pre>
<div class="Pp"></div>
We can reduce some of the looping through slices
<div class="Pp"></div>
<pre>
    for ($x = 4; $x &lt;= 8; $x++) {
        push @newAoA, [ @{ $AoA[$x] } [ 7..12 ] ];
    }
</pre>
<div class="Pp"></div>
If you were into Schwartzian Transforms, you would probably have selected map
  for that
<div class="Pp"></div>
<pre>
    @newAoA = map { [ @{ $AoA[$_] } [ 7..12 ] ] } 4 .. 8;
</pre>
<div class="Pp"></div>
Although if your manager accused you of seeking job security (or rapid
  insecurity) through inscrutable code, it would be hard to argue. :-) If I were
  you, I'd put that in a function:
<div class="Pp"></div>
<pre>
    @newAoA = splice_2D( \@AoA, 4 =&gt; 8, 7 =&gt; 12 );
    sub splice_2D {
        my $lrr = shift;        # ref to array of array refs!
        my ($x_lo, $x_hi,
            $y_lo, $y_hi) = @_;
        return map {
            [ @{ $lrr-&gt;[$_] } [ $y_lo .. $y_hi ] ]
        } $x_lo .. $x_hi;
    }
</pre>
<h1 class="Sh" title="Sh" id="SEE_ALSO"><a class="selflink" href="#SEE_ALSO">SEE
  ALSO</a></h1>
perldata, perlref, perldsc
<h1 class="Sh" title="Sh" id="AUTHOR"><a class="selflink" href="#AUTHOR">AUTHOR</a></h1>
Tom Christiansen &lt; <i>tchrist@perl.com</i>&gt;
<div class="Pp"></div>
Last update: Tue Apr 26 18:30:55 MDT 2011</div>
<table class="foot">
  <tr>
    <td class="foot-date">2013-03-04</td>
    <td class="foot-os">perl v5.16.3</td>
  </tr>
</table>
</body>
</html>
