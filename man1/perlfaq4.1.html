<!DOCTYPE html>
<html>
<head>
  <meta charset="utf-8"/>
  <style>
    table.head, table.foot { width: 100%; }
    td.head-rtitle, td.foot-os { text-align: right; }
    td.head-vol { text-align: center; }
    div.Pp { margin: 1ex 0ex; }
  </style>
  <title>PERLFAQ4(1)</title>
</head>
<body>
<table class="head">
  <tr>
    <td class="head-ltitle">PERLFAQ4(1)</td>
    <td class="head-vol">Perl Programmers Reference Guide</td>
    <td class="head-rtitle">PERLFAQ4(1)</td>
  </tr>
</table>
<div class="manual-text">
<h1 class="Sh" title="Sh" id="NAME"><a class="selflink" href="#NAME">NAME</a></h1>
perlfaq4 - Data Manipulation
<h1 class="Sh" title="Sh" id="DESCRIPTION"><a class="selflink" href="#DESCRIPTION">DESCRIPTION</a></h1>
This section of the FAQ answers questions related to manipulating numbers,
  dates, strings, arrays, hashes, and miscellaneous data issues.
<h1 class="Sh" title="Sh" id="Data:_Numbers"><a class="selflink" href="#Data:_Numbers">Data:
  Numbers</a></h1>
<h2 class="Ss" title="Ss" id="Why_am_I_getting_long_decimals_(eg,_19.9499999999999)_instead_of_the_numbers_I_should_be_getting_(eg,_19.95)?"><a class="selflink" href="#Why_am_I_getting_long_decimals_(eg,_19.9499999999999)_instead_of_the_numbers_I_should_be_getting_(eg,_19.95)?">Why
  am I getting long decimals (eg, 19.9499999999999) instead of the numbers I
  should be getting (eg, 19.95)?</a></h2>
For the long explanation, see David Goldberg's &quot;What Every Computer
  Scientist Should Know About Floating-Point Arithmetic&quot;
  (&lt;http://web.cse.msu.edu/~cse320/Documents/FloatingPoint.pdf&gt;).
<div class="Pp"></div>
Internally, your computer represents floating-point numbers in binary. Digital
  (as in powers of two) computers cannot store all numbers exactly. Some real
  numbers lose precision in the process. This is a problem with how computers
  store numbers and affects all computer languages, not just Perl.
<div class="Pp"></div>
perlnumber shows the gory details of number representations and conversions.
<div class="Pp"></div>
To limit the number of decimal places in your numbers, you can use the
  &quot;printf&quot; or &quot;sprintf&quot; function. See &quot;Floating-point
  Arithmetic&quot; in perlop for more details.
<div class="Pp"></div>
<pre>
    printf &quot;%.2f&quot;, 10/3;
    my $number = sprintf &quot;%.2f&quot;, 10/3;
</pre>
<h2 class="Ss" title="Ss" id="Why_is_int()_broken?"><a class="selflink" href="#Why_is_int()_broken?">Why
  is <i>int()</i> broken?</a></h2>
Your &quot;int()&quot; is most probably working just fine. It's the numbers that
  aren't quite what you think.
<div class="Pp"></div>
First, see the answer to &quot;Why am I getting long decimals (eg,
  19.9499999999999) instead of the numbers I should be getting (eg,
  19.95)?&quot;.
<div class="Pp"></div>
For example, this
<div class="Pp"></div>
<pre>
    print int(0.6/0.2-2), &quot;\n&quot;;
</pre>
<div class="Pp"></div>
will in most computers print 0, not 1, because even such simple numbers as 0.6
  and 0.2 cannot be presented exactly by floating-point numbers. What you think
  in the above as 'three' is really more like 2.9999999999999995559.
<h2 class="Ss" title="Ss" id="Why_isn't_my_octal_data_interpreted_correctly?"><a class="selflink" href="#Why_isn't_my_octal_data_interpreted_correctly?">Why
  isn't my octal data interpreted correctly?</a></h2>
(contributed by brian d foy)
<div class="Pp"></div>
You're probably trying to convert a string to a number, which Perl only converts
  as a decimal number. When Perl converts a string to a number, it ignores
  leading spaces and zeroes, then assumes the rest of the digits are in base 10:
<div class="Pp"></div>
<pre>
    my $string = '0644';
    print $string + 0;  # prints 644
    print $string + 44; # prints 688, certainly not octal!
</pre>
<div class="Pp"></div>
This problem usually involves one of the Perl built-ins that has the same name a
  Unix command that uses octal numbers as arguments on the command line. In this
  example, &quot;chmod&quot; on the command line knows that its first argument
  is octal because that's what it does:
<div class="Pp"></div>
<pre>
    %prompt&gt; chmod 644 file
</pre>
<div class="Pp"></div>
If you want to use the same literal digits (644) in Perl, you have to tell Perl
  to treat them as octal numbers either by prefixing the digits with a 0 or
  using &quot;oct&quot;:
<div class="Pp"></div>
<pre>
    chmod(     0644, $filename );  # right, has leading zero
    chmod( oct(644), $filename );  # also correct
</pre>
<div class="Pp"></div>
The problem comes in when you take your numbers from something that Perl thinks
  is a string, such as a command line argument in @ARGV:
<div class="Pp"></div>
<pre>
    chmod( $ARGV[0],      $filename );  # wrong, even if &quot;0644&quot;
    chmod( oct($ARGV[0]), $filename );  # correct, treat string as octal
</pre>
<div class="Pp"></div>
You can always check the value you're using by printing it in octal notation to
  ensure it matches what you think it should be. Print it in octal and decimal
  format:
<div class="Pp"></div>
<pre>
    printf &quot;0%o %d&quot;, $number, $number;
</pre>
<h2 class="Ss" title="Ss" id="Does_Perl_have_a_round()_function?_What_about_ceil()_and_floor()?_Trig_functions?"><a class="selflink" href="#Does_Perl_have_a_round()_function?_What_about_ceil()_and_floor()?_Trig_functions?">Does
  Perl have a <i>round()</i> function? What about <i>ceil()</i> and
  <i>floor()</i>? Trig functions?</a></h2>
Remember that &quot;int()&quot; merely truncates toward 0. For rounding to a
  certain number of digits, &quot;sprintf()&quot; or &quot;printf()&quot; is
  usually the easiest route.
<div class="Pp"></div>
<pre>
    printf(&quot;%.3f&quot;, 3.1415926535);   # prints 3.142
</pre>
<div class="Pp"></div>
The POSIX module (part of the standard Perl distribution) implements
  &quot;ceil()&quot;, &quot;floor()&quot;, and a number of other mathematical
  and trigonometric functions.
<div class="Pp"></div>
<pre>
    use POSIX;
    my $ceil   = ceil(3.5);   # 4
    my $floor  = floor(3.5);  # 3
</pre>
<div class="Pp"></div>
In 5.000 to 5.003 perls, trigonometry was done in the Math::Complex module. With
  5.004, the Math::Trig module (part of the standard Perl distribution)
  implements the trigonometric functions. Internally it uses the Math::Complex
  module and some functions can break out from the real axis into the complex
  plane, for example the inverse sine of 2.
<div class="Pp"></div>
Rounding in financial applications can have serious implications, and the
  rounding method used should be specified precisely. In these cases, it
  probably pays not to trust whichever system of rounding is being used by Perl,
  but instead to implement the rounding function you need yourself.
<div class="Pp"></div>
To see why, notice how you'll still have an issue on half-way-point alternation:
<div class="Pp"></div>
<pre>
    for (my $i = 0; $i &lt; 1.01; $i += 0.05) { printf &quot;%.1f &quot;,$i}
    0.0 0.1 0.1 0.2 0.2 0.2 0.3 0.3 0.4 0.4 0.5 0.5 0.6 0.7 0.7
    0.8 0.8 0.9 0.9 1.0 1.0
</pre>
<div class="Pp"></div>
Don't blame Perl. It's the same as in C. IEEE says we have to do this. Perl
  numbers whose absolute values are integers under 2**31 (on 32-bit machines)
  will work pretty much like mathematical integers. Other numbers are not
  guaranteed.
<h2 class="Ss" title="Ss" id="How_do_I_convert_between_numeric_representations/bases/radixes?"><a class="selflink" href="#How_do_I_convert_between_numeric_representations/bases/radixes?">How
  do I convert between numeric representations/bases/radixes?</a></h2>
As always with Perl there is more than one way to do it. Below are a few
  examples of approaches to making common conversions between number
  representations. This is intended to be representational rather than
  exhaustive.
<div class="Pp"></div>
Some of the examples later in perlfaq4 use the Bit::Vector module from CPAN. The
  reason you might choose Bit::Vector over the perl built-in functions is that
  it works with numbers of ANY size, that it is optimized for speed on some
  operations, and for at least some programmers the notation might be familiar.
<dl class="Bl-tag">
  <dt class="It-tag">How do I convert hexadecimal into decimal</dt>
  <dd class="It-tag">Using perl's built in conversion of &quot;0x&quot;
      notation:
    <div style="height: 1.00em;">&#x00A0;</div>
    <pre>
    my $dec = 0xDEADBEEF;
    </pre>
    <div style="height: 1.00em;">&#x00A0;</div>
    Using the &quot;hex&quot; function:
    <div style="height: 1.00em;">&#x00A0;</div>
    <pre>
    my $dec = hex(&quot;DEADBEEF&quot;);
    </pre>
    <div style="height: 1.00em;">&#x00A0;</div>
    Using &quot;pack&quot;:
    <div style="height: 1.00em;">&#x00A0;</div>
    <pre>
    my $dec = unpack(&quot;N&quot;, pack(&quot;H8&quot;, substr(&quot;0&quot; x 8 . &quot;DEADBEEF&quot;, -8)));
    </pre>
    <div style="height: 1.00em;">&#x00A0;</div>
    Using the CPAN module &quot;Bit::Vector&quot;:
    <div style="height: 1.00em;">&#x00A0;</div>
    <pre>
    use Bit::Vector;
    my $vec = Bit::Vector-&gt;new_Hex(32, &quot;DEADBEEF&quot;);
    my $dec = $vec-&gt;to_Dec();
    </pre>
  </dd>
</dl>
<dl class="Bl-tag">
  <dt class="It-tag">How do I convert from decimal to hexadecimal</dt>
  <dd class="It-tag">Using &quot;sprintf&quot;:
    <div style="height: 1.00em;">&#x00A0;</div>
    <pre>
    my $hex = sprintf(&quot;%X&quot;, 3735928559); # upper case A-F
    my $hex = sprintf(&quot;%x&quot;, 3735928559); # lower case a-f
    </pre>
    <div style="height: 1.00em;">&#x00A0;</div>
    Using &quot;unpack&quot;:
    <div style="height: 1.00em;">&#x00A0;</div>
    <pre>
    my $hex = unpack(&quot;H*&quot;, pack(&quot;N&quot;, 3735928559));
    </pre>
    <div style="height: 1.00em;">&#x00A0;</div>
    Using Bit::Vector:
    <div style="height: 1.00em;">&#x00A0;</div>
    <pre>
    use Bit::Vector;
    my $vec = Bit::Vector-&gt;new_Dec(32, -559038737);
    my $hex = $vec-&gt;to_Hex();
    </pre>
    <div style="height: 1.00em;">&#x00A0;</div>
    And Bit::Vector supports odd bit counts:
    <div style="height: 1.00em;">&#x00A0;</div>
    <pre>
    use Bit::Vector;
    my $vec = Bit::Vector-&gt;new_Dec(33, 3735928559);
    $vec-&gt;Resize(32); # suppress leading 0 if unwanted
    my $hex = $vec-&gt;to_Hex();
    </pre>
  </dd>
</dl>
<dl class="Bl-tag">
  <dt class="It-tag">How do I convert from octal to decimal</dt>
  <dd class="It-tag">Using Perl's built in conversion of numbers with leading
      zeros:
    <div style="height: 1.00em;">&#x00A0;</div>
    <pre>
    my $dec = 033653337357; # note the leading 0!
    </pre>
    <div style="height: 1.00em;">&#x00A0;</div>
    Using the &quot;oct&quot; function:
    <div style="height: 1.00em;">&#x00A0;</div>
    <pre>
    my $dec = oct(&quot;33653337357&quot;);
    </pre>
    <div style="height: 1.00em;">&#x00A0;</div>
    Using Bit::Vector:
    <div style="height: 1.00em;">&#x00A0;</div>
    <pre>
    use Bit::Vector;
    my $vec = Bit::Vector-&gt;new(32);
    $vec-&gt;Chunk_List_Store(3, split(//, reverse &quot;33653337357&quot;));
    my $dec = $vec-&gt;to_Dec();
    </pre>
  </dd>
</dl>
<dl class="Bl-tag">
  <dt class="It-tag">How do I convert from decimal to octal</dt>
  <dd class="It-tag">Using &quot;sprintf&quot;:
    <div style="height: 1.00em;">&#x00A0;</div>
    <pre>
    my $oct = sprintf(&quot;%o&quot;, 3735928559);
    </pre>
    <div style="height: 1.00em;">&#x00A0;</div>
    Using Bit::Vector:
    <div style="height: 1.00em;">&#x00A0;</div>
    <pre>
    use Bit::Vector;
    my $vec = Bit::Vector-&gt;new_Dec(32, -559038737);
    my $oct = reverse join('', $vec-&gt;Chunk_List_Read(3));
    </pre>
  </dd>
</dl>
<dl class="Bl-tag">
  <dt class="It-tag">How do I convert from binary to decimal</dt>
  <dd class="It-tag">Perl 5.6 lets you write binary numbers directly with the
      &quot;0b&quot; notation:
    <div style="height: 1.00em;">&#x00A0;</div>
    <pre>
    my $number = 0b10110110;
    </pre>
    <div style="height: 1.00em;">&#x00A0;</div>
    Using &quot;oct&quot;:
    <div style="height: 1.00em;">&#x00A0;</div>
    <pre>
    my $input = &quot;10110110&quot;;
    my $decimal = oct( &quot;0b$input&quot; );
    </pre>
    <div style="height: 1.00em;">&#x00A0;</div>
    Using &quot;pack&quot; and &quot;ord&quot;:
    <div style="height: 1.00em;">&#x00A0;</div>
    <pre>
    my $decimal = ord(pack('B8', '10110110'));
    </pre>
    <div style="height: 1.00em;">&#x00A0;</div>
    Using &quot;pack&quot; and &quot;unpack&quot; for larger strings:
    <div style="height: 1.00em;">&#x00A0;</div>
    <pre>
    my $int = unpack(&quot;N&quot;, pack(&quot;B32&quot;,
    substr(&quot;0&quot; x 32 . &quot;11110101011011011111011101111&quot;, -32)));
    my $dec = sprintf(&quot;%d&quot;, $int);
    # substr() is used to left-pad a 32-character string with zeros.
    </pre>
    <div style="height: 1.00em;">&#x00A0;</div>
    Using Bit::Vector:
    <div style="height: 1.00em;">&#x00A0;</div>
    <pre>
    my $vec = Bit::Vector-&gt;new_Bin(32, &quot;11011110101011011011111011101111&quot;);
    my $dec = $vec-&gt;to_Dec();
    </pre>
  </dd>
</dl>
<dl class="Bl-tag">
  <dt class="It-tag">How do I convert from decimal to binary</dt>
  <dd class="It-tag">Using &quot;sprintf&quot; (perl 5.6+):
    <div style="height: 1.00em;">&#x00A0;</div>
    <pre>
    my $bin = sprintf(&quot;%b&quot;, 3735928559);
    </pre>
    <div style="height: 1.00em;">&#x00A0;</div>
    Using &quot;unpack&quot;:
    <div style="height: 1.00em;">&#x00A0;</div>
    <pre>
    my $bin = unpack(&quot;B*&quot;, pack(&quot;N&quot;, 3735928559));
    </pre>
    <div style="height: 1.00em;">&#x00A0;</div>
    Using Bit::Vector:
    <div style="height: 1.00em;">&#x00A0;</div>
    <pre>
    use Bit::Vector;
    my $vec = Bit::Vector-&gt;new_Dec(32, -559038737);
    my $bin = $vec-&gt;to_Bin();
    </pre>
    <div style="height: 1.00em;">&#x00A0;</div>
    The remaining transformations (e.g. hex -&gt; oct, bin -&gt; hex, etc.) are
      left as an exercise to the inclined reader.</dd>
</dl>
<h2 class="Ss" title="Ss" id="Why_doesn't_&amp;_work_the_way_I_want_it_to?"><a class="selflink" href="#Why_doesn't_&amp;_work_the_way_I_want_it_to?">Why
  doesn't &amp; work the way I want it to?</a></h2>
The behavior of binary arithmetic operators depends on whether they're used on
  numbers or strings. The operators treat a string as a series of bits and work
  with that (the string &quot;3&quot; is the bit pattern 00110011). The
  operators work with the binary form of a number (the number 3 is treated as
  the bit pattern 00000011).
<div class="Pp"></div>
So, saying &quot;11 &amp; 3&quot; performs the &quot;and&quot; operation on
  numbers (yielding 3). Saying &quot;11&quot; &amp; &quot;3&quot; performs the
  &quot;and&quot; operation on strings (yielding &quot;1&quot;).
<div class="Pp"></div>
Most problems with &quot;&amp;&quot; and &quot;|&quot; arise because the
  programmer thinks they have a number but really it's a string or vice versa.
  To avoid this, stringify the arguments explicitly (using &quot;&quot; or
  &quot;qq()&quot;) or convert them to numbers explicitly (using
  &quot;0+$arg&quot;). The rest arise because the programmer says:
<div class="Pp"></div>
<pre>
    if (&quot;\020\020&quot; &amp; &quot;\101\101&quot;) {
        # ...
    }
</pre>
<div class="Pp"></div>
but a string consisting of two null bytes (the result of &quot;\020\020&quot;
  &amp; &quot;\101\101&quot;) is not a false value in Perl. You need:
<div class="Pp"></div>
<pre>
    if ( (&quot;\020\020&quot; &amp; &quot;\101\101&quot;) !~ /[^\000]/) {
        # ...
    }
</pre>
<h2 class="Ss" title="Ss" id="How_do_I_multiply_matrices?"><a class="selflink" href="#How_do_I_multiply_matrices?">How
  do I multiply matrices?</a></h2>
Use the Math::Matrix or Math::MatrixReal modules (available from CPAN) or the
  PDL extension (also available from CPAN).
<h2 class="Ss" title="Ss" id="How_do_I_perform_an_operation_on_a_series_of_integers?"><a class="selflink" href="#How_do_I_perform_an_operation_on_a_series_of_integers?">How
  do I perform an operation on a series of integers?</a></h2>
To call a function on each element in an array, and collect the results, use:
<div class="Pp"></div>
<pre>
    my @results = map { my_func($_) } @array;
</pre>
<div class="Pp"></div>
For example:
<div class="Pp"></div>
<pre>
    my @triple = map { 3 * $_ } @single;
</pre>
<div class="Pp"></div>
To call a function on each element of an array, but ignore the results:
<div class="Pp"></div>
<pre>
    foreach my $iterator (@array) {
        some_func($iterator);
    }
</pre>
<div class="Pp"></div>
To call a function on each integer in a (small) range, you <b>can</b> use:
<div class="Pp"></div>
<pre>
    my @results = map { some_func($_) } (5 .. 25);
</pre>
<div class="Pp"></div>
but you should be aware that in this form, the &quot;..&quot; operator creates a
  list of all integers in the range, which can take a lot of memory for large
  ranges. However, the problem does not occur when using &quot;..&quot; within a
  &quot;for&quot; loop, because in that case the range operator is optimized to
  <i>iterate</i> over the range, without creating the entire list. So
<div class="Pp"></div>
<pre>
    my @results = ();
    for my $i (5 .. 500_005) {
        push(@results, some_func($i));
    }
</pre>
<div class="Pp"></div>
or even
<div class="Pp"></div>
<pre>
   push(@results, some_func($_)) for 5 .. 500_005;
</pre>
<div class="Pp"></div>
will not create an intermediate list of 500,000 integers.
<h2 class="Ss" title="Ss" id="How_can_I_output_Roman_numerals?"><a class="selflink" href="#How_can_I_output_Roman_numerals?">How
  can I output Roman numerals?</a></h2>
Get the http://www.cpan.org/modules/by-module/Roman
  &lt;http://www.cpan.org/modules/by-module/Roman&gt; module.
<h2 class="Ss" title="Ss" id="Why_aren't_my_random_numbers_random?"><a class="selflink" href="#Why_aren't_my_random_numbers_random?">Why
  aren't my random numbers random?</a></h2>
If you're using a version of Perl before 5.004, you must call &quot;srand&quot;
  once at the start of your program to seed the random number generator.
<div class="Pp"></div>
<pre>
     BEGIN { srand() if $] &lt; 5.004 }
</pre>
<div class="Pp"></div>
5.004 and later automatically call &quot;srand&quot; at the beginning. Don't
  call &quot;srand&quot; more than once--you make your numbers less random,
  rather than more.
<div class="Pp"></div>
Computers are good at being predictable and bad at being random (despite
  appearances caused by bugs in your programs :-). The <i>random</i> article in
  the &quot;Far More Than You Ever Wanted To Know&quot; collection in
  &lt;http://www.cpan.org/misc/olddoc/FMTEYEWTK.tgz&gt;, courtesy of Tom
  Phoenix, talks more about this. John von Neumann said, &quot;Anyone who
  attempts to generate random numbers by deterministic means is, of course,
  living in a state of sin.&quot;
<div class="Pp"></div>
Perl relies on the underlying system for the implementation of &quot;rand&quot;
  and &quot;srand&quot;; on some systems, the generated numbers are not random
  enough (especially on Windows : see
  &lt;http://www.perlmonks.org/?node_id=803632&gt;). Several CPAN modules in the
  &quot;Math&quot; namespace implement better pseudorandom generators; see for
  example Math::Random::MT (&quot;Mersenne Twister&quot;, fast), or
  Math::TrulyRandom (uses the imperfections in the system's timer to generate
  random numbers, which is rather slow). More algorithms for random numbers are
  described in &quot;Numerical Recipes in C&quot; at &lt;http://www.nr.com/&gt;
<h2 class="Ss" title="Ss" id="How_do_I_get_a_random_number_between_X_and_Y?"><a class="selflink" href="#How_do_I_get_a_random_number_between_X_and_Y?">How
  do I get a random number between X and Y?</a></h2>
To get a random number between two values, you can use the &quot;rand()&quot;
  built-in to get a random number between 0 and 1. From there, you shift that
  into the range that you want.
<div class="Pp"></div>
&quot;rand($x)&quot; returns a number such that &quot;0 &lt;= rand($x) &lt;
  $x&quot;. Thus what you want to have perl figure out is a random number in the
  range from 0 to the difference between your <i>X</i> and <i>Y</i>.
<div class="Pp"></div>
That is, to get a number between 10 and 15, inclusive, you want a random number
  between 0 and 5 that you can then add to 10.
<div class="Pp"></div>
<pre>
    my $number = 10 + int rand( 15-10+1 ); # ( 10,11,12,13,14, or 15 )
</pre>
<div class="Pp"></div>
Hence you derive the following simple function to abstract that. It selects a
  random integer between the two given integers (inclusive), For example:
  &quot;random_int_between(50,120)&quot;.
<div class="Pp"></div>
<pre>
    sub random_int_between {
        my($min, $max) = @_;
        # Assumes that the two arguments are integers themselves!
        return $min if $min == $max;
        ($min, $max) = ($max, $min)  if  $min &gt; $max;
        return $min + int rand(1 + $max - $min);
    }
</pre>
<h1 class="Sh" title="Sh" id="Data:_Dates"><a class="selflink" href="#Data:_Dates">Data:
  Dates</a></h1>
<h2 class="Ss" title="Ss" id="How_do_I_find_the_day_or_week_of_the_year?"><a class="selflink" href="#How_do_I_find_the_day_or_week_of_the_year?">How
  do I find the day or week of the year?</a></h2>
The day of the year is in the list returned by the &quot;localtime&quot;
  function. Without an argument &quot;localtime&quot; uses the current time.
<div class="Pp"></div>
<pre>
    my $day_of_year = (localtime)[7];
</pre>
<div class="Pp"></div>
The POSIX module can also format a date as the day of the year or week of the
  year.
<div class="Pp"></div>
<pre>
    use POSIX qw/strftime/;
    my $day_of_year  = strftime &quot;%j&quot;, localtime;
    my $week_of_year = strftime &quot;%W&quot;, localtime;
</pre>
<div class="Pp"></div>
To get the day of year for any date, use POSIX's &quot;mktime&quot; to get a
  time in epoch seconds for the argument to &quot;localtime&quot;.
<div class="Pp"></div>
<pre>
    use POSIX qw/mktime strftime/;
    my $week_of_year = strftime &quot;%W&quot;,
        localtime( mktime( 0, 0, 0, 18, 11, 87 ) );
</pre>
<div class="Pp"></div>
You can also use Time::Piece, which comes with Perl and provides a
  &quot;localtime&quot; that returns an object:
<div class="Pp"></div>
<pre>
    use Time::Piece;
    my $day_of_year  = localtime-&gt;yday;
    my $week_of_year = localtime-&gt;week;
</pre>
<div class="Pp"></div>
The Date::Calc module provides two functions to calculate these, too:
<div class="Pp"></div>
<pre>
    use Date::Calc;
    my $day_of_year  = Day_of_Year(  1987, 12, 18 );
    my $week_of_year = Week_of_Year( 1987, 12, 18 );
</pre>
<h2 class="Ss" title="Ss" id="How_do_I_find_the_current_century_or_millennium?"><a class="selflink" href="#How_do_I_find_the_current_century_or_millennium?">How
  do I find the current century or millennium?</a></h2>
Use the following simple functions:
<div class="Pp"></div>
<pre>
    sub get_century    {
        return int((((localtime(shift || time))[5] + 1999))/100);
    }
    sub get_millennium {
        return 1+int((((localtime(shift || time))[5] + 1899))/1000);
    }
</pre>
<div class="Pp"></div>
On some systems, the POSIX module's &quot;strftime()&quot; function has been
  extended in a non-standard way to use a %C format, which they sometimes claim
  is the &quot;century&quot;. It isn't, because on most such systems, this is
  only the first two digits of the four-digit year, and thus cannot be used to
  determine reliably the current century or millennium.
<h2 class="Ss" title="Ss" id="How_can_I_compare_two_dates_and_find_the_difference?"><a class="selflink" href="#How_can_I_compare_two_dates_and_find_the_difference?">How
  can I compare two dates and find the difference?</a></h2>
(contributed by brian d foy)
<div class="Pp"></div>
You could just store all your dates as a number and then subtract. Life isn't
  always that simple though.
<div class="Pp"></div>
The Time::Piece module, which comes with Perl, replaces localtime with a version
  that returns an object. It also overloads the comparison operators so you can
  compare them directly:
<div class="Pp"></div>
<pre>
    use Time::Piece;
    my $date1 = localtime( $some_time );
    my $date2 = localtime( $some_other_time );
    if( $date1 &lt; $date2 ) {
        print &quot;The date was in the past\n&quot;;
    }
</pre>
<div class="Pp"></div>
You can also get differences with a subtraction, which returns a Time::Seconds
  object:
<div class="Pp"></div>
<pre>
    my $diff = $date1 - $date2;
    print &quot;The difference is &quot;, $date_diff-&gt;days, &quot; days\n&quot;;
</pre>
<div class="Pp"></div>
If you want to work with formatted dates, the Date::Manip, Date::Calc, or
  DateTime modules can help you.
<h2 class="Ss" title="Ss" id="How_can_I_take_a_string_and_turn_it_into_epoch_seconds?"><a class="selflink" href="#How_can_I_take_a_string_and_turn_it_into_epoch_seconds?">How
  can I take a string and turn it into epoch seconds?</a></h2>
If it's a regular enough string that it always has the same format, you can
  split it up and pass the parts to &quot;timelocal&quot; in the standard
  Time::Local module. Otherwise, you should look into the Date::Calc,
  Date::Parse, and Date::Manip modules from CPAN.
<h2 class="Ss" title="Ss" id="How_can_I_find_the_Julian_Day?"><a class="selflink" href="#How_can_I_find_the_Julian_Day?">How
  can I find the Julian Day?</a></h2>
(contributed by brian d foy and Dave Cross)
<div class="Pp"></div>
You can use the Time::Piece module, part of the Standard Library, which can
  convert a date/time to a Julian Day:
<div class="Pp"></div>
<pre>
    $ perl -MTime::Piece -le 'print localtime-&gt;julian_day'
    2455607.7959375
</pre>
<div class="Pp"></div>
Or the modified Julian Day:
<div class="Pp"></div>
<pre>
    $ perl -MTime::Piece -le 'print localtime-&gt;mjd'
    55607.2961226851
</pre>
<div class="Pp"></div>
Or even the day of the year (which is what some people think of as a Julian
  day):
<div class="Pp"></div>
<pre>
    $ perl -MTime::Piece -le 'print localtime-&gt;yday'
    45
</pre>
<div class="Pp"></div>
You can also do the same things with the DateTime module:
<div class="Pp"></div>
<pre>
    $ perl -MDateTime -le'print DateTime-&gt;today-&gt;jd'
    2453401.5
    $ perl -MDateTime -le'print DateTime-&gt;today-&gt;mjd'
    53401
    $ perl -MDateTime -le'print DateTime-&gt;today-&gt;doy'
    31
</pre>
<div class="Pp"></div>
You can use the Time::JulianDay module available on CPAN. Ensure that you really
  want to find a Julian day, though, as many people have different ideas about
  Julian days (see &lt;http://www.hermetic.ch/cal_stud/jdn.htm&gt; for
  instance):
<div class="Pp"></div>
<pre>
    $  perl -MTime::JulianDay -le 'print local_julian_day( time )'
    55608
</pre>
<h2 class="Ss" title="Ss" id="How_do_I_find_yesterday's_date?"><a class="selflink" href="#How_do_I_find_yesterday's_date?">How
  do I find yesterday's date?</a></h2>
(contributed by brian d foy)
<div class="Pp"></div>
To do it correctly, you can use one of the &quot;Date&quot; modules since they
  work with calendars instead of times. The DateTime module makes it simple, and
  give you the same time of day, only the day before, despite daylight saving
  time changes:
<div class="Pp"></div>
<pre>
    use DateTime;
    my $yesterday = DateTime-&gt;now-&gt;subtract( days =&gt; 1 );
    print &quot;Yesterday was $yesterday\n&quot;;
</pre>
<div class="Pp"></div>
You can also use the Date::Calc module using its &quot;Today_and_Now&quot;
  function.
<div class="Pp"></div>
<pre>
    use Date::Calc qw( Today_and_Now Add_Delta_DHMS );
    my @date_time = Add_Delta_DHMS( Today_and_Now(), -1, 0, 0, 0 );
    print &quot;@date_time\n&quot;;
</pre>
<div class="Pp"></div>
Most people try to use the time rather than the calendar to figure out dates,
  but that assumes that days are twenty-four hours each. For most people, there
  are two days a year when they aren't: the switch to and from summer time
  throws this off. For example, the rest of the suggestions will be wrong
  sometimes:
<div class="Pp"></div>
Starting with Perl 5.10, Time::Piece and Time::Seconds are part of the standard
  distribution, so you might think that you could do something like this:
<div class="Pp"></div>
<pre>
    use Time::Piece;
    use Time::Seconds;
    my $yesterday = localtime() - ONE_DAY; # WRONG
    print &quot;Yesterday was $yesterday\n&quot;;
</pre>
<div class="Pp"></div>
The Time::Piece module exports a new &quot;localtime&quot; that returns an
  object, and Time::Seconds exports the &quot;ONE_DAY&quot; constant that is a
  set number of seconds. This means that it always gives the time 24 hours ago,
  which is not always yesterday. This can cause problems around the end of
  daylight saving time when there's one day that is 25 hours long.
<div class="Pp"></div>
You have the same problem with Time::Local, which will give the wrong answer for
  those same special cases:
<div class="Pp"></div>
<pre>
    # contributed by Gunnar Hjalmarsson
     use Time::Local;
     my $today = timelocal 0, 0, 12, ( localtime )[3..5];
     my ($d, $m, $y) = ( localtime $today-86400 )[3..5]; # WRONG
     printf &quot;Yesterday: %d-%02d-%02d\n&quot;, $y+1900, $m+1, $d;
</pre>
<h2 class="Ss" title="Ss" id="Does_Perl_have_a_Year_2000_or_2038_problem?_Is_Perl_Y2K_compliant?"><a class="selflink" href="#Does_Perl_have_a_Year_2000_or_2038_problem?_Is_Perl_Y2K_compliant?">Does
  Perl have a Year 2000 or 2038 problem? Is Perl Y2K compliant?</a></h2>
(contributed by brian d foy)
<div class="Pp"></div>
Perl itself never had a Y2K problem, although that never stopped people from
  creating Y2K problems on their own. See the documentation for
  &quot;localtime&quot; for its proper use.
<div class="Pp"></div>
Starting with Perl 5.12, &quot;localtime&quot; and &quot;gmtime&quot; can handle
  dates past 03:14:08 January 19, 2038, when a 32-bit based time would overflow.
  You still might get a warning on a 32-bit &quot;perl&quot;:
<div class="Pp"></div>
<pre>
    % perl5.12 -E 'say scalar localtime( 0x9FFF_FFFFFFFF )'
    Integer overflow in hexadecimal number at -e line 1.
    Wed Nov  1 19:42:39 5576711
</pre>
<div class="Pp"></div>
On a 64-bit &quot;perl&quot;, you can get even larger dates for those really
  long running projects:
<div class="Pp"></div>
<pre>
    % perl5.12 -E 'say scalar gmtime( 0x9FFF_FFFFFFFF )'
    Thu Nov  2 00:42:39 5576711
</pre>
<div class="Pp"></div>
You're still out of luck if you need to keep track of decaying protons though.
<h1 class="Sh" title="Sh" id="Data:_Strings"><a class="selflink" href="#Data:_Strings">Data:
  Strings</a></h1>
<h2 class="Ss" title="Ss" id="How_do_I_validate_input?"><a class="selflink" href="#How_do_I_validate_input?">How
  do I validate input?</a></h2>
(contributed by brian d foy)
<div class="Pp"></div>
There are many ways to ensure that values are what you expect or want to accept.
  Besides the specific examples that we cover in the perlfaq, you can also look
  at the modules with &quot;Assert&quot; and &quot;Validate&quot; in their
  names, along with other modules such as Regexp::Common.
<div class="Pp"></div>
Some modules have validation for particular types of input, such as
  Business::ISBN, Business::CreditCard, Email::Valid, and Data::Validate::IP.
<h2 class="Ss" title="Ss" id="How_do_I_unescape_a_string?"><a class="selflink" href="#How_do_I_unescape_a_string?">How
  do I unescape a string?</a></h2>
It depends just what you mean by &quot;escape&quot;. URL escapes are dealt with
  in perlfaq9. Shell escapes with the backslash (&quot;\&quot;) character are
  removed with
<div class="Pp"></div>
<pre>
    s/\\(.)/$1/g;
</pre>
<div class="Pp"></div>
This won't expand &quot;\n&quot; or &quot;\t&quot; or any other special escapes.
<h2 class="Ss" title="Ss" id="How_do_I_remove_consecutive_pairs_of_characters?"><a class="selflink" href="#How_do_I_remove_consecutive_pairs_of_characters?">How
  do I remove consecutive pairs of characters?</a></h2>
(contributed by brian d foy)
<div class="Pp"></div>
You can use the substitution operator to find pairs of characters (or runs of
  characters) and replace them with a single instance. In this substitution, we
  find a character in &quot;(.)&quot;. The memory parentheses store the matched
  character in the back-reference &quot;\g1&quot; and we use that to require
  that the same thing immediately follow it. We replace that part of the string
  with the character in $1.
<div class="Pp"></div>
<pre>
    s/(.)\g1/$1/g;
</pre>
<div class="Pp"></div>
We can also use the transliteration operator, &quot;tr///&quot;. In this
  example, the search list side of our &quot;tr///&quot; contains nothing, but
  the &quot;c&quot; option complements that so it contains everything. The
  replacement list also contains nothing, so the transliteration is almost a
  no-op since it won't do any replacements (or more exactly, replace the
  character with itself). However, the &quot;s&quot; option squashes duplicated
  and consecutive characters in the string so a character does not show up next
  to itself
<div class="Pp"></div>
<pre>
    my $str = 'Haarlem';   # in the Netherlands
    $str =~ tr///cs;       # Now Harlem, like in New York
</pre>
<h2 class="Ss" title="Ss" id="How_do_I_expand_function_calls_in_a_string?"><a class="selflink" href="#How_do_I_expand_function_calls_in_a_string?">How
  do I expand function calls in a string?</a></h2>
(contributed by brian d foy)
<div class="Pp"></div>
This is documented in perlref, and although it's not the easiest thing to read,
  it does work. In each of these examples, we call the function inside the
  braces used to dereference a reference. If we have more than one return value,
  we can construct and dereference an anonymous array. In this case, we call the
  function in list context.
<div class="Pp"></div>
<pre>
    print &quot;The time values are @{ [localtime] }.\n&quot;;
</pre>
<div class="Pp"></div>
If we want to call the function in scalar context, we have to do a bit more
  work. We can really have any code we like inside the braces, so we simply have
  to end with the scalar reference, although how you do that is up to you, and
  you can use code inside the braces. Note that the use of parens creates a list
  context, so we need &quot;scalar&quot; to force the scalar context on the
  function:
<div class="Pp"></div>
<pre>
    print &quot;The time is ${\(scalar localtime)}.\n&quot;
    print &quot;The time is ${ my $x = localtime; \$x }.\n&quot;;
</pre>
<div class="Pp"></div>
If your function already returns a reference, you don't need to create the
  reference yourself.
<div class="Pp"></div>
<pre>
    sub timestamp { my $t = localtime; \$t }
    print &quot;The time is ${ timestamp() }.\n&quot;;
</pre>
<div class="Pp"></div>
The &quot;Interpolation&quot; module can also do a lot of magic for you. You can
  specify a variable name, in this case &quot;E&quot;, to set up a tied hash
  that does the interpolation for you. It has several other methods to do this
  as well.
<div class="Pp"></div>
<pre>
    use Interpolation E =&gt; 'eval';
    print &quot;The time values are $E{localtime()}.\n&quot;;
</pre>
<div class="Pp"></div>
In most cases, it is probably easier to simply use string concatenation, which
  also forces scalar context.
<div class="Pp"></div>
<pre>
    print &quot;The time is &quot; . localtime() . &quot;.\n&quot;;
</pre>
<h2 class="Ss" title="Ss" id="How_do_I_find_matching/nesting_anything?"><a class="selflink" href="#How_do_I_find_matching/nesting_anything?">How
  do I find matching/nesting anything?</a></h2>
To find something between two single characters, a pattern like
  &quot;/x([^x]*)x/&quot; will get the intervening bits in $1. For multiple
  ones, then something more like &quot;/alpha(.*?)omega/&quot; would be needed.
  For nested patterns and/or balanced expressions, see the so-called (?PARNO)
  construct (available since perl 5.10). The CPAN module Regexp::Common can help
  to build such regular expressions (see in particular Regexp::Common::balanced
  and Regexp::Common::delimited).
<div class="Pp"></div>
More complex cases will require to write a parser, probably using a parsing
  module from CPAN, like Regexp::Grammars, Parse::RecDescent, Parse::Yapp,
  Text::Balanced, or Marpa::XS.
<h2 class="Ss" title="Ss" id="How_do_I_reverse_a_string?"><a class="selflink" href="#How_do_I_reverse_a_string?">How
  do I reverse a string?</a></h2>
Use &quot;reverse()&quot; in scalar context, as documented in
  &quot;reverse&quot; in perlfunc.
<div class="Pp"></div>
<pre>
    my $reversed = reverse $string;
</pre>
<h2 class="Ss" title="Ss" id="How_do_I_expand_tabs_in_a_string?"><a class="selflink" href="#How_do_I_expand_tabs_in_a_string?">How
  do I expand tabs in a string?</a></h2>
You can do it yourself:
<div class="Pp"></div>
<pre>
    1 while $string =~ s/\t+/' ' x (length($&amp;) * 8 - length($`) % 8)/e;
</pre>
<div class="Pp"></div>
Or you can just use the Text::Tabs module (part of the standard Perl
  distribution).
<div class="Pp"></div>
<pre>
    use Text::Tabs;
    my @expanded_lines = expand(@lines_with_tabs);
</pre>
<h2 class="Ss" title="Ss" id="How_do_I_reformat_a_paragraph?"><a class="selflink" href="#How_do_I_reformat_a_paragraph?">How
  do I reformat a paragraph?</a></h2>
Use Text::Wrap (part of the standard Perl distribution):
<div class="Pp"></div>
<pre>
    use Text::Wrap;
    print wrap(&quot;\t&quot;, '  ', @paragraphs);
</pre>
<div class="Pp"></div>
The paragraphs you give to Text::Wrap should not contain embedded newlines.
  Text::Wrap doesn't justify the lines (flush-right).
<div class="Pp"></div>
Or use the CPAN module Text::Autoformat. Formatting files can be easily done by
  making a shell alias, like so:
<div class="Pp"></div>
<pre>
    alias fmt=&quot;perl -i -MText::Autoformat -n0777 \
        -e 'print autoformat $_, {all=&gt;1}' $*&quot;
</pre>
<div class="Pp"></div>
See the documentation for Text::Autoformat to appreciate its many capabilities.
<h2 class="Ss" title="Ss" id="How_can_I_access_or_change_N_characters_of_a_string?"><a class="selflink" href="#How_can_I_access_or_change_N_characters_of_a_string?">How
  can I access or change N characters of a string?</a></h2>
You can access the first characters of a string with <i>substr()</i>. To get the
  first character, for example, start at position 0 and grab the string of
  length 1.
<div class="Pp"></div>
<pre>
    my $string = &quot;Just another Perl Hacker&quot;;
    my $first_char = substr( $string, 0, 1 );  #  'J'
</pre>
<div class="Pp"></div>
To change part of a string, you can use the optional fourth argument which is
  the replacement string.
<div class="Pp"></div>
<pre>
    substr( $string, 13, 4, &quot;Perl 5.8.0&quot; );
</pre>
<div class="Pp"></div>
You can also use <i>substr()</i> as an lvalue.
<div class="Pp"></div>
<pre>
    substr( $string, 13, 4 ) =  &quot;Perl 5.8.0&quot;;
</pre>
<h2 class="Ss" title="Ss" id="How_do_I_change_the_Nth_occurrence_of_something?"><a class="selflink" href="#How_do_I_change_the_Nth_occurrence_of_something?">How
  do I change the Nth occurrence of something?</a></h2>
You have to keep track of N yourself. For example, let's say you want to change
  the fifth occurrence of &quot;whoever&quot; or &quot;whomever&quot; into
  &quot;whosoever&quot; or &quot;whomsoever&quot;, case insensitively. These all
  assume that $_ contains the string to be altered.
<div class="Pp"></div>
<pre>
    $count = 0;
    s{((whom?)ever)}{
    ++$count == 5       # is it the 5th?
        ? &quot;${2}soever&quot;  # yes, swap
        : $1            # renege and leave it there
        }ige;
</pre>
<div class="Pp"></div>
In the more general case, you can use the &quot;/g&quot; modifier in a
  &quot;while&quot; loop, keeping count of matches.
<div class="Pp"></div>
<pre>
    $WANT = 3;
    $count = 0;
    $_ = &quot;One fish two fish red fish blue fish&quot;;
    while (/(\w+)\s+fish\b/gi) {
        if (++$count == $WANT) {
            print &quot;The third fish is a $1 one.\n&quot;;
        }
    }
</pre>
<div class="Pp"></div>
That prints out: &quot;The third fish is a red one.&quot; You can also use a
  repetition count and repeated pattern like this:
<div class="Pp"></div>
<pre>
    /(?:\w+\s+fish\s+){2}(\w+)\s+fish/i;
</pre>
<h2 class="Ss" title="Ss" id="How_can_I_count_the_number_of_occurrences_of_a_substring_within_a_string?"><a class="selflink" href="#How_can_I_count_the_number_of_occurrences_of_a_substring_within_a_string?">How
  can I count the number of occurrences of a substring within a string?</a></h2>
There are a number of ways, with varying efficiency. If you want a count of a
  certain single character (X) within a string, you can use the
  &quot;tr///&quot; function like so:
<div class="Pp"></div>
<pre>
    my $string = &quot;ThisXlineXhasXsomeXx'sXinXit&quot;;
    my $count = ($string =~ tr/X//);
    print &quot;There are $count X characters in the string&quot;;
</pre>
<div class="Pp"></div>
This is fine if you are just looking for a single character. However, if you are
  trying to count multiple character substrings within a larger string,
  &quot;tr///&quot; won't work. What you can do is wrap a <i>while()</i> loop
  around a global pattern match. For example, let's count negative integers:
<div class="Pp"></div>
<pre>
    my $string = &quot;-9 55 48 -2 23 -76 4 14 -44&quot;;
    my $count = 0;
    while ($string =~ /-\d+/g) { $count++ }
    print &quot;There are $count negative numbers in the string&quot;;
</pre>
<div class="Pp"></div>
Another version uses a global match in list context, then assigns the result to
  a scalar, producing a count of the number of matches.
<div class="Pp"></div>
<pre>
    my $count = () = $string =~ /-\d+/g;
</pre>
<h2 class="Ss" title="Ss" id="How_do_I_capitalize_all_the_words_on_one_line?"><a class="selflink" href="#How_do_I_capitalize_all_the_words_on_one_line?">How
  do I capitalize all the words on one line?</a></h2>
(contributed by brian d foy)
<div class="Pp"></div>
Damian Conway's Text::Autoformat handles all of the thinking for you.
<div class="Pp"></div>
<pre>
    use Text::Autoformat;
    my $x = &quot;Dr. Strangelove or: How I Learned to Stop &quot;.
      &quot;Worrying and Love the Bomb&quot;;
    print $x, &quot;\n&quot;;
    for my $style (qw( sentence title highlight )) {
        print autoformat($x, { case =&gt; $style }), &quot;\n&quot;;
    }
</pre>
<div class="Pp"></div>
How do you want to capitalize those words?
<div class="Pp"></div>
<pre>
    FRED AND BARNEY'S LODGE        # all uppercase
    Fred And Barney's Lodge        # title case
    Fred and Barney's Lodge        # highlight case
</pre>
<div class="Pp"></div>
It's not as easy a problem as it looks. How many words do you think are in
  there? Wait for it... wait for it.... If you answered 5 you're right. Perl
  words are groups of &quot;\w+&quot;, but that's not what you want to
  capitalize. How is Perl supposed to know not to capitalize that &quot;s&quot;
  after the apostrophe? You could try a regular expression:
<div class="Pp"></div>
<pre>
    $string =~ s/ (
                 (^\w)    #at the beginning of the line
                   |      # or
                 (\s\w)   #preceded by whitespace
                   )
                /\U$1/xg;
    $string =~ s/([\w']+)/\u\L$1/g;
</pre>
<div class="Pp"></div>
Now, what if you don't want to capitalize that &quot;and&quot;? Just use
  Text::Autoformat and get on with the next problem. :)
<h2 class="Ss" title="Ss" id="How_can_I_split_a_[character]-delimited_string_except_when_inside_[character]?"><a class="selflink" href="#How_can_I_split_a_[character]-delimited_string_except_when_inside_[character]?">How
  can I split a [character]-delimited string except when inside
  [character]?</a></h2>
Several modules can handle this sort of parsing--Text::Balanced, Text::CSV,
  Text::CSV_XS, and Text::ParseWords, among others.
<div class="Pp"></div>
Take the example case of trying to split a string that is comma-separated into
  its different fields. You can't use &quot;split(/,/)&quot; because you
  shouldn't split if the comma is inside quotes. For example, take a data line
  like this:
<div class="Pp"></div>
<pre>
    SAR001,&quot;&quot;,&quot;Cimetrix, Inc&quot;,&quot;Bob Smith&quot;,&quot;CAM&quot;,N,8,1,0,7,&quot;Error, Core Dumped&quot;
</pre>
<div class="Pp"></div>
Due to the restriction of the quotes, this is a fairly complex problem.
  Thankfully, we have Jeffrey Friedl, author of <i>Mastering Regular
  Expressions</i>, to handle these for us. He suggests (assuming your string is
  contained in $text):
<div class="Pp"></div>
<pre>
     my @new = ();
     push(@new, $+) while $text =~ m{
         &quot;([^\&quot;\\]*(?:\\.[^\&quot;\\]*)*)&quot;,? # groups the phrase inside the quotes
        | ([^,]+),?
        | ,
     }gx;
     push(@new, undef) if substr($text,-1,1) eq ',';
</pre>
<div class="Pp"></div>
If you want to represent quotation marks inside a quotation-mark-delimited
  field, escape them with backslashes (eg, &quot;like \&quot;this\&quot;&quot;.
<div class="Pp"></div>
Alternatively, the Text::ParseWords module (part of the standard Perl
  distribution) lets you say:
<div class="Pp"></div>
<pre>
    use Text::ParseWords;
    @new = quotewords(&quot;,&quot;, 0, $text);
</pre>
<div class="Pp"></div>
For parsing or generating CSV, though, using Text::CSV rather than implementing
  it yourself is highly recommended; you'll save yourself odd bugs popping up
  later by just using code which has already been tried and tested in production
  for years.
<h2 class="Ss" title="Ss" id="How_do_I_strip_blank_space_from_the_beginning/end_of_a_string?"><a class="selflink" href="#How_do_I_strip_blank_space_from_the_beginning/end_of_a_string?">How
  do I strip blank space from the beginning/end of a string?</a></h2>
(contributed by brian d foy)
<div class="Pp"></div>
A substitution can do this for you. For a single line, you want to replace all
  the leading or trailing whitespace with nothing. You can do that with a pair
  of substitutions:
<div class="Pp"></div>
<pre>
    s/^\s+//;
    s/\s+$//;
</pre>
<div class="Pp"></div>
You can also write that as a single substitution, although it turns out the
  combined statement is slower than the separate ones. That might not matter to
  you, though:
<div class="Pp"></div>
<pre>
    s/^\s+|\s+$//g;
</pre>
<div class="Pp"></div>
In this regular expression, the alternation matches either at the beginning or
  the end of the string since the anchors have a lower precedence than the
  alternation. With the &quot;/g&quot; flag, the substitution makes all possible
  matches, so it gets both. Remember, the trailing newline matches the
  &quot;\s+&quot;, and the &quot;$&quot; anchor can match to the absolute end of
  the string, so the newline disappears too. Just add the newline to the output,
  which has the added benefit of preserving &quot;blank&quot; (consisting
  entirely of whitespace) lines which the &quot;^\s+&quot; would remove all by
  itself:
<div class="Pp"></div>
<pre>
    while( &lt;&gt; ) {
        s/^\s+|\s+$//g;
        print &quot;$_\n&quot;;
    }
</pre>
<div class="Pp"></div>
For a multi-line string, you can apply the regular expression to each logical
  line in the string by adding the &quot;/m&quot; flag (for
  &quot;multi-line&quot;). With the &quot;/m&quot; flag, the &quot;$&quot;
  matches <i>before</i> an embedded newline, so it doesn't remove it. This
  pattern still removes the newline at the end of the string:
<div class="Pp"></div>
<pre>
    $string =~ s/^\s+|\s+$//gm;
</pre>
<div class="Pp"></div>
Remember that lines consisting entirely of whitespace will disappear, since the
  first part of the alternation can match the entire string and replace it with
  nothing. If you need to keep embedded blank lines, you have to do a little
  more work. Instead of matching any whitespace (since that includes a newline),
  just match the other whitespace:
<div class="Pp"></div>
<pre>
    $string =~ s/^[\t\f ]+|[\t\f ]+$//mg;
</pre>
<h2 class="Ss" title="Ss" id="How_do_I_pad_a_string_with_blanks_or_pad_a_number_with_zeroes?"><a class="selflink" href="#How_do_I_pad_a_string_with_blanks_or_pad_a_number_with_zeroes?">How
  do I pad a string with blanks or pad a number with zeroes?</a></h2>
In the following examples, $pad_len is the length to which you wish to pad the
  string, $text or $num contains the string to be padded, and $pad_char contains
  the padding character. You can use a single character string constant instead
  of the $pad_char variable if you know what it is in advance. And in the same
  way you can use an integer in place of $pad_len if you know the pad length in
  advance.
<div class="Pp"></div>
The simplest method uses the &quot;sprintf&quot; function. It can pad on the
  left or right with blanks and on the left with zeroes and it will not truncate
  the result. The &quot;pack&quot; function can only pad strings on the right
  with blanks and it will truncate the result to a maximum length of $pad_len.
<div class="Pp"></div>
<pre>
    # Left padding a string with blanks (no truncation):
    my $padded = sprintf(&quot;%${pad_len}s&quot;, $text);
    my $padded = sprintf(&quot;%*s&quot;, $pad_len, $text);  # same thing
    # Right padding a string with blanks (no truncation):
    my $padded = sprintf(&quot;%-${pad_len}s&quot;, $text);
    my $padded = sprintf(&quot;%-*s&quot;, $pad_len, $text); # same thing
    # Left padding a number with 0 (no truncation):
    my $padded = sprintf(&quot;%0${pad_len}d&quot;, $num);
    my $padded = sprintf(&quot;%0*d&quot;, $pad_len, $num); # same thing
    # Right padding a string with blanks using pack (will truncate):
    my $padded = pack(&quot;A$pad_len&quot;,$text);
</pre>
<div class="Pp"></div>
If you need to pad with a character other than blank or zero you can use one of
  the following methods. They all generate a pad string with the &quot;x&quot;
  operator and combine that with $text. These methods do not truncate $text.
<div class="Pp"></div>
Left and right padding with any character, creating a new string:
<div class="Pp"></div>
<pre>
    my $padded = $pad_char x ( $pad_len - length( $text ) ) . $text;
    my $padded = $text . $pad_char x ( $pad_len - length( $text ) );
</pre>
<div class="Pp"></div>
Left and right padding with any character, modifying $text directly:
<div class="Pp"></div>
<pre>
    substr( $text, 0, 0 ) = $pad_char x ( $pad_len - length( $text ) );
    $text .= $pad_char x ( $pad_len - length( $text ) );
</pre>
<h2 class="Ss" title="Ss" id="How_do_I_extract_selected_columns_from_a_string?"><a class="selflink" href="#How_do_I_extract_selected_columns_from_a_string?">How
  do I extract selected columns from a string?</a></h2>
(contributed by brian d foy)
<div class="Pp"></div>
If you know the columns that contain the data, you can use &quot;substr&quot; to
  extract a single column.
<div class="Pp"></div>
<pre>
    my $column = substr( $line, $start_column, $length );
</pre>
<div class="Pp"></div>
You can use &quot;split&quot; if the columns are separated by whitespace or some
  other delimiter, as long as whitespace or the delimiter cannot appear as part
  of the data.
<div class="Pp"></div>
<pre>
    my $line    = ' fred barney   betty   ';
    my @columns = split /\s+/, $line;
        # ( '', 'fred', 'barney', 'betty' );
    my $line    = 'fred||barney||betty';
    my @columns = split /\|/, $line;
        # ( 'fred', '', 'barney', '', 'betty' );
</pre>
<div class="Pp"></div>
If you want to work with comma-separated values, don't do this since that format
  is a bit more complicated. Use one of the modules that handle that format,
  such as Text::CSV, Text::CSV_XS, or Text::CSV_PP.
<div class="Pp"></div>
If you want to break apart an entire line of fixed columns, you can use
  &quot;unpack&quot; with the A (ASCII) format. By using a number after the
  format specifier, you can denote the column width. See the &quot;pack&quot;
  and &quot;unpack&quot; entries in perlfunc for more details.
<div class="Pp"></div>
<pre>
    my @fields = unpack( $line, &quot;A8 A8 A8 A16 A4&quot; );
</pre>
<div class="Pp"></div>
Note that spaces in the format argument to &quot;unpack&quot; do not denote
  literal spaces. If you have space separated data, you may want
  &quot;split&quot; instead.
<h2 class="Ss" title="Ss" id="How_do_I_find_the_soundex_value_of_a_string?"><a class="selflink" href="#How_do_I_find_the_soundex_value_of_a_string?">How
  do I find the soundex value of a string?</a></h2>
(contributed by brian d foy)
<div class="Pp"></div>
You can use the &quot;Text::Soundex&quot; module. If you want to do fuzzy or
  close matching, you might also try the String::Approx, and Text::Metaphone,
  and Text::DoubleMetaphone modules.
<h2 class="Ss" title="Ss" id="How_can_I_expand_variables_in_text_strings?"><a class="selflink" href="#How_can_I_expand_variables_in_text_strings?">How
  can I expand variables in text strings?</a></h2>
(contributed by brian d foy)
<div class="Pp"></div>
If you can avoid it, don't, or if you can use a templating system, such as
  Text::Template or Template Toolkit, do that instead. You might even be able to
  get the job done with &quot;sprintf&quot; or &quot;printf&quot;:
<div class="Pp"></div>
<pre>
    my $string = sprintf 'Say hello to %s and %s', $foo, $bar;
</pre>
<div class="Pp"></div>
However, for the one-off simple case where I don't want to pull out a full
  templating system, I'll use a string that has two Perl scalar variables in it.
  In this example, I want to expand $foo and $bar to their variable's values:
<div class="Pp"></div>
<pre>
    my $foo = 'Fred';
    my $bar = 'Barney';
    $string = 'Say hello to $foo and $bar';
</pre>
<div class="Pp"></div>
One way I can do this involves the substitution operator and a double
  &quot;/e&quot; flag. The first &quot;/e&quot; evaluates $1 on the replacement
  side and turns it into $foo. The second /e starts with $foo and replaces it
  with its value. $foo, then, turns into 'Fred', and that's finally what's left
  in the string:
<div class="Pp"></div>
<pre>
    $string =~ s/(\$\w+)/$1/eeg; # 'Say hello to Fred and Barney'
</pre>
<div class="Pp"></div>
The &quot;/e&quot; will also silently ignore violations of strict, replacing
  undefined variable names with the empty string. Since I'm using the
  &quot;/e&quot; flag (twice even!), I have all of the same security problems I
  have with &quot;eval&quot; in its string form. If there's something odd in
  $foo, perhaps something like &quot;@{[ system &quot;rm -rf /&quot; ]}&quot;,
  then I could get myself in trouble.
<div class="Pp"></div>
To get around the security problem, I could also pull the values from a hash
  instead of evaluating variable names. Using a single &quot;/e&quot;, I can
  check the hash to ensure the value exists, and if it doesn't, I can replace
  the missing value with a marker, in this case &quot;???&quot; to signal that I
  missed something:
<div class="Pp"></div>
<pre>
    my $string = 'This has $foo and $bar';
    my %Replacements = (
        foo  =&gt; 'Fred',
        );
    # $string =~ s/\$(\w+)/$Replacements{$1}/g;
    $string =~ s/\$(\w+)/
        exists $Replacements{$1} ? $Replacements{$1} : '???'
        /eg;
    print $string;
</pre>
<h2 class="Ss" title="Ss" id="What's_wrong_with_always_quoting_&quot;$vars&quot;?"><a class="selflink" href="#What's_wrong_with_always_quoting_&quot;$vars&quot;?">What's
  wrong with always quoting &quot;$vars&quot;?</a></h2>
The problem is that those double-quotes force stringification--coercing numbers
  and references into strings--even when you don't want them to be strings.
  Think of it this way: double-quote expansion is used to produce new strings.
  If you already have a string, why do you need more?
<div class="Pp"></div>
If you get used to writing odd things like these:
<div class="Pp"></div>
<pre>
    print &quot;$var&quot;;       # BAD
    my $new = &quot;$old&quot;;       # BAD
    somefunc(&quot;$var&quot;);    # BAD
</pre>
<div class="Pp"></div>
You'll be in trouble. Those should (in 99.8% of the cases) be the simpler and
  more direct:
<div class="Pp"></div>
<pre>
    print $var;
    my $new = $old;
    somefunc($var);
</pre>
<div class="Pp"></div>
Otherwise, besides slowing you down, you're going to break code when the thing
  in the scalar is actually neither a string nor a number, but a reference:
<div class="Pp"></div>
<pre>
    func(\@array);
    sub func {
        my $aref = shift;
        my $oref = &quot;$aref&quot;;  # WRONG
    }
</pre>
<div class="Pp"></div>
You can also get into subtle problems on those few operations in Perl that
  actually do care about the difference between a string and a number, such as
  the magical &quot;++&quot; autoincrement operator or the <i>syscall()</i>
  function.
<div class="Pp"></div>
Stringification also destroys arrays.
<div class="Pp"></div>
<pre>
    my @lines = `command`;
    print &quot;@lines&quot;;     # WRONG - extra blanks
    print @lines;       # right
</pre>
<h2 class="Ss" title="Ss" id="Why_don't_my_&lt;&lt;HERE_documents_work?"><a class="selflink" href="#Why_don't_my_&lt;&lt;HERE_documents_work?">Why
  don't my &lt;&lt;HERE documents work?</a></h2>
Here documents are found in perlop. Check for these three things:
<dl class="Bl-tag">
  <dt class="It-tag">There must be no space after the &lt;&lt; part.</dt>
  <dd class="It-tag"></dd>
</dl>
<dl class="Bl-tag">
  <dt class="It-tag">There (probably) should be a semicolon at the end of the
    opening token</dt>
  <dd class="It-tag"></dd>
</dl>
<dl class="Bl-tag">
  <dt class="It-tag">You can't (easily) have any space in front of the tag.</dt>
  <dd class="It-tag"></dd>
</dl>
<dl class="Bl-tag">
  <dt class="It-tag">There needs to be at least a line separator after the end
    token.</dt>
  <dd class="It-tag"></dd>
</dl>
<div class="Pp"></div>
If you want to indent the text in the here document, you can do this:
<div class="Pp"></div>
<pre>
    # all in one
    (my $VAR = &lt;&lt;HERE_TARGET) =~ s/^\s+//gm;
        your text
        goes here
    HERE_TARGET
</pre>
<div class="Pp"></div>
But the HERE_TARGET must still be flush against the margin. If you want that
  indented also, you'll have to quote in the indentation.
<div class="Pp"></div>
<pre>
    (my $quote = &lt;&lt;'    FINIS') =~ s/^\s+//gm;
            ...we will have peace, when you and all your works have
            perished--and the works of your dark master to whom you
            would deliver us. You are a liar, Saruman, and a corrupter
            of men's hearts. --Theoden in /usr/src/perl/taint.c
        FINIS
    $quote =~ s/\s+--/\n--/;
</pre>
<div class="Pp"></div>
A nice general-purpose fixer-upper function for indented here documents follows.
  It expects to be called with a here document as its argument. It looks to see
  whether each line begins with a common substring, and if so, strips that
  substring off. Otherwise, it takes the amount of leading whitespace found on
  the first line and removes that much off each subsequent line.
<div class="Pp"></div>
<pre>
    sub fix {
        local $_ = shift;
        my ($white, $leader);  # common whitespace and common leading string
        if (/^\s*(?:([^\w\s]+)(\s*).*\n)(?:\s*\g1\g2?.*\n)+$/) {
            ($white, $leader) = ($2, quotemeta($1));
        } else {
            ($white, $leader) = (/^(\s+)/, '');
        }
        s/^\s*?$leader(?:$white)?//gm;
        return $_;
    }
</pre>
<div class="Pp"></div>
This works with leading special strings, dynamically determined:
<div class="Pp"></div>
<pre>
    my $remember_the_main = fix&lt;&lt;'    MAIN_INTERPRETER_LOOP';
    @@@ int
    @@@ runops() {
    @@@     SAVEI32(runlevel);
    @@@     runlevel++;
    @@@     while ( op = (*op-&gt;op_ppaddr)() );
    @@@     TAINT_NOT;
    @@@     return 0;
    @@@ }
    MAIN_INTERPRETER_LOOP
</pre>
<div class="Pp"></div>
Or with a fixed amount of leading whitespace, with remaining indentation
  correctly preserved:
<div class="Pp"></div>
<pre>
    my $poem = fix&lt;&lt;EVER_ON_AND_ON;
       Now far ahead the Road has gone,
      And I must follow, if I can,
       Pursuing it with eager feet,
      Until it joins some larger way
       Where many paths and errands meet.
      And whither then? I cannot say.
        --Bilbo in /usr/src/perl/pp_ctl.c
    EVER_ON_AND_ON
</pre>
<h1 class="Sh" title="Sh" id="Data:_Arrays"><a class="selflink" href="#Data:_Arrays">Data:
  Arrays</a></h1>
<h2 class="Ss" title="Ss" id="What_is_the_difference_between_a_list_and_an_array?"><a class="selflink" href="#What_is_the_difference_between_a_list_and_an_array?">What
  is the difference between a list and an array?</a></h2>
(contributed by brian d foy)
<div class="Pp"></div>
A list is a fixed collection of scalars. An array is a variable that holds a
  variable collection of scalars. An array can supply its collection for list
  operations, so list operations also work on arrays:
<div class="Pp"></div>
<pre>
    # slices
    ( 'dog', 'cat', 'bird' )[2,3];
    @animals[2,3];
    # iteration
    foreach ( qw( dog cat bird ) ) { ... }
    foreach ( @animals ) { ... }
    my @three = grep { length == 3 } qw( dog cat bird );
    my @three = grep { length == 3 } @animals;
    # supply an argument list
    wash_animals( qw( dog cat bird ) );
    wash_animals( @animals );
</pre>
<div class="Pp"></div>
Array operations, which change the scalars, rearranges them, or adds or
  subtracts some scalars, only work on arrays. These can't work on a list, which
  is fixed. Array operations include &quot;shift&quot;, &quot;unshift&quot;,
  &quot;push&quot;, &quot;pop&quot;, and &quot;splice&quot;.
<div class="Pp"></div>
An array can also change its length:
<div class="Pp"></div>
<pre>
    $#animals = 1;  # truncate to two elements
    $#animals = 10000; # pre-extend to 10,001 elements
</pre>
<div class="Pp"></div>
You can change an array element, but you can't change a list element:
<div class="Pp"></div>
<pre>
    $animals[0] = 'Rottweiler';
    qw( dog cat bird )[0] = 'Rottweiler'; # syntax error!
    foreach ( @animals ) {
        s/^d/fr/;  # works fine
    }
    foreach ( qw( dog cat bird ) ) {
        s/^d/fr/;  # Error! Modification of read only value!
    }
</pre>
<div class="Pp"></div>
However, if the list element is itself a variable, it appears that you can
  change a list element. However, the list element is the variable, not the
  data. You're not changing the list element, but something the list element
  refers to. The list element itself doesn't change: it's still the same
  variable.
<div class="Pp"></div>
You also have to be careful about context. You can assign an array to a scalar
  to get the number of elements in the array. This only works for arrays,
  though:
<div class="Pp"></div>
<pre>
    my $count = @animals;  # only works with arrays
</pre>
<div class="Pp"></div>
If you try to do the same thing with what you think is a list, you get a quite
  different result. Although it looks like you have a list on the righthand
  side, Perl actually sees a bunch of scalars separated by a comma:
<div class="Pp"></div>
<pre>
    my $scalar = ( 'dog', 'cat', 'bird' );  # $scalar gets bird
</pre>
<div class="Pp"></div>
Since you're assigning to a scalar, the righthand side is in scalar context. The
  comma operator (yes, it's an operator!) in scalar context evaluates its
  lefthand side, throws away the result, and evaluates it's righthand side and
  returns the result. In effect, that list-lookalike assigns to $scalar it's
  rightmost value. Many people mess this up because they choose a list-lookalike
  whose last element is also the count they expect:
<div class="Pp"></div>
<pre>
    my $scalar = ( 1, 2, 3 );  # $scalar gets 3, accidentally
</pre>
<h2 class="Ss" title="Ss" id="What_is_the_difference_between_$array[1]_and_@array[1]?"><a class="selflink" href="#What_is_the_difference_between_$array[1]_and_@array[1]?">What
  is the difference between $array[1] and @array[1]?</a></h2>
(contributed by brian d foy)
<div class="Pp"></div>
The difference is the sigil, that special character in front of the array name.
  The &quot;$&quot; sigil means &quot;exactly one item&quot;, while the
  &quot;@&quot; sigil means &quot;zero or more items&quot;. The &quot;$&quot;
  gets you a single scalar, while the &quot;@&quot; gets you a list.
<div class="Pp"></div>
The confusion arises because people incorrectly assume that the sigil denotes
  the variable type.
<div class="Pp"></div>
The $array[1] is a single-element access to the array. It's going to return the
  item in index 1 (or undef if there is no item there). If you intend to get
  exactly one element from the array, this is the form you should use.
<div class="Pp"></div>
The @array[1] is an array slice, although it has only one index. You can pull
  out multiple elements simultaneously by specifying additional indices as a
  list, like @array[1,4,3,0].
<div class="Pp"></div>
Using a slice on the lefthand side of the assignment supplies list context to
  the righthand side. This can lead to unexpected results. For instance, if you
  want to read a single line from a filehandle, assigning to a scalar value is
  fine:
<div class="Pp"></div>
<pre>
    $array[1] = &lt;STDIN&gt;;
</pre>
<div class="Pp"></div>
However, in list context, the line input operator returns all of the lines as a
  list. The first line goes into @array[1] and the rest of the lines
  mysteriously disappear:
<div class="Pp"></div>
<pre>
    @array[1] = &lt;STDIN&gt;;  # most likely not what you want
</pre>
<div class="Pp"></div>
Either the &quot;use warnings&quot; pragma or the <b>-w</b> flag will warn you
  when you use an array slice with a single index.
<h2 class="Ss" title="Ss" id="How_can_I_remove_duplicate_elements_from_a_list_or_array?"><a class="selflink" href="#How_can_I_remove_duplicate_elements_from_a_list_or_array?">How
  can I remove duplicate elements from a list or array?</a></h2>
(contributed by brian d foy)
<div class="Pp"></div>
Use a hash. When you think the words &quot;unique&quot; or
  &quot;duplicated&quot;, think &quot;hash keys&quot;.
<div class="Pp"></div>
If you don't care about the order of the elements, you could just create the
  hash then extract the keys. It's not important how you create that hash: just
  that you use &quot;keys&quot; to get the unique elements.
<div class="Pp"></div>
<pre>
    my %hash   = map { $_, 1 } @array;
    # or a hash slice: @hash{ @array } = ();
    # or a foreach: $hash{$_} = 1 foreach ( @array );
    my @unique = keys %hash;
</pre>
<div class="Pp"></div>
If you want to use a module, try the &quot;uniq&quot; function from
  List::MoreUtils. In list context it returns the unique elements, preserving
  their order in the list. In scalar context, it returns the number of unique
  elements.
<div class="Pp"></div>
<pre>
    use List::MoreUtils qw(uniq);
    my @unique = uniq( 1, 2, 3, 4, 4, 5, 6, 5, 7 ); # 1,2,3,4,5,6,7
    my $unique = uniq( 1, 2, 3, 4, 4, 5, 6, 5, 7 ); # 7
</pre>
<div class="Pp"></div>
You can also go through each element and skip the ones you've seen before. Use a
  hash to keep track. The first time the loop sees an element, that element has
  no key in %Seen. The &quot;next&quot; statement creates the key and
  immediately uses its value, which is &quot;undef&quot;, so the loop continues
  to the &quot;push&quot; and increments the value for that key. The next time
  the loop sees that same element, its key exists in the hash <i>and</i> the
  value for that key is true (since it's not 0 or &quot;undef&quot;), so the
  next skips that iteration and the loop goes to the next element.
<div class="Pp"></div>
<pre>
    my @unique = ();
    my %seen   = ();
    foreach my $elem ( @array ) {
        next if $seen{ $elem }++;
        push @unique, $elem;
    }
</pre>
<div class="Pp"></div>
You can write this more briefly using a grep, which does the same thing.
<div class="Pp"></div>
<pre>
    my %seen = ();
    my @unique = grep { ! $seen{ $_ }++ } @array;
</pre>
<h2 class="Ss" title="Ss" id="How_can_I_tell_whether_a_certain_element_is_contained_in_a_list_or_array?"><a class="selflink" href="#How_can_I_tell_whether_a_certain_element_is_contained_in_a_list_or_array?">How
  can I tell whether a certain element is contained in a list or array?</a></h2>
(portions of this answer contributed by Anno Siegel and brian d foy)
<div class="Pp"></div>
Hearing the word &quot;in&quot; is an <i>in</i>dication that you probably should
  have used a hash, not a list or array, to store your data. Hashes are designed
  to answer this question quickly and efficiently. Arrays aren't.
<div class="Pp"></div>
That being said, there are several ways to approach this. In Perl 5.10 and
  later, you can use the smart match operator to check that an item is contained
  in an array or a hash:
<div class="Pp"></div>
<pre>
    use 5.010;
    if( $item ~~ @array ) {
        say &quot;The array contains $item&quot;
    }
    if( $item ~~ %hash ) {
        say &quot;The hash contains $item&quot;
    }
</pre>
<div class="Pp"></div>
With earlier versions of Perl, you have to do a bit more work. If you are going
  to make this query many times over arbitrary string values, the fastest way is
  probably to invert the original array and maintain a hash whose keys are the
  first array's values:
<div class="Pp"></div>
<pre>
    my @blues = qw/azure cerulean teal turquoise lapis-lazuli/;
    my %is_blue = ();
    for (@blues) { $is_blue{$_} = 1 }
</pre>
<div class="Pp"></div>
Now you can check whether $is_blue{$some_color}. It might have been a good idea
  to keep the blues all in a hash in the first place.
<div class="Pp"></div>
If the values are all small integers, you could use a simple indexed array. This
  kind of an array will take up less space:
<div class="Pp"></div>
<pre>
    my @primes = (2, 3, 5, 7, 11, 13, 17, 19, 23, 29, 31);
    my @is_tiny_prime = ();
    for (@primes) { $is_tiny_prime[$_] = 1 }
    # or simply  @istiny_prime[@primes] = (1) x @primes;
</pre>
<div class="Pp"></div>
Now you check whether $is_tiny_prime[$some_number].
<div class="Pp"></div>
If the values in question are integers instead of strings, you can save quite a
  lot of space by using bit strings instead:
<div class="Pp"></div>
<pre>
    my @articles = ( 1..10, 150..2000, 2017 );
    undef $read;
    for (@articles) { vec($read,$_,1) = 1 }
</pre>
<div class="Pp"></div>
Now check whether &quot;vec($read,$n,1)&quot; is true for some $n.
<div class="Pp"></div>
These methods guarantee fast individual tests but require a re-organization of
  the original list or array. They only pay off if you have to test multiple
  values against the same array.
<div class="Pp"></div>
If you are testing only once, the standard module List::Util exports the
  function &quot;first&quot; for this purpose. It works by stopping once it
  finds the element. It's written in C for speed, and its Perl equivalent looks
  like this subroutine:
<div class="Pp"></div>
<pre>
    sub first (&amp;@) {
        my $code = shift;
        foreach (@_) {
            return $_ if &amp;{$code}();
        }
        undef;
    }
</pre>
<div class="Pp"></div>
If speed is of little concern, the common idiom uses grep in scalar context
  (which returns the number of items that passed its condition) to traverse the
  entire list. This does have the benefit of telling you how many matches it
  found, though.
<div class="Pp"></div>
<pre>
    my $is_there = grep $_ eq $whatever, @array;
</pre>
<div class="Pp"></div>
If you want to actually extract the matching elements, simply use grep in list
  context.
<div class="Pp"></div>
<pre>
    my @matches = grep $_ eq $whatever, @array;
</pre>
<h2 class="Ss" title="Ss" id="How_do_I_compute_the_difference_of_two_arrays?_How_do_I_compute_the_intersection_of_two_arrays?"><a class="selflink" href="#How_do_I_compute_the_difference_of_two_arrays?_How_do_I_compute_the_intersection_of_two_arrays?">How
  do I compute the difference of two arrays? How do I compute the intersection
  of two arrays?</a></h2>
Use a hash. Here's code to do both and more. It assumes that each element is
  unique in a given array:
<div class="Pp"></div>
<pre>
    my (@union, @intersection, @difference);
    my %count = ();
    foreach my $element (@array1, @array2) { $count{$element}++ }
    foreach my $element (keys %count) {
        push @union, $element;
        push @{ $count{$element} &gt; 1 ? \@intersection : \@difference }, $element;
    }
</pre>
<div class="Pp"></div>
Note that this is the <i>symmetric difference</i>, that is, all elements in
  either A or in B but not in both. Think of it as an xor operation.
<h2 class="Ss" title="Ss" id="How_do_I_test_whether_two_arrays_or_hashes_are_equal?"><a class="selflink" href="#How_do_I_test_whether_two_arrays_or_hashes_are_equal?">How
  do I test whether two arrays or hashes are equal?</a></h2>
With Perl 5.10 and later, the smart match operator can give you the answer with
  the least amount of work:
<div class="Pp"></div>
<pre>
    use 5.010;
    if( @array1 ~~ @array2 ) {
        say &quot;The arrays are the same&quot;;
    }
    if( %hash1 ~~ %hash2 ) # doesn't check values!  {
        say &quot;The hash keys are the same&quot;;
    }
</pre>
<div class="Pp"></div>
The following code works for single-level arrays. It uses a stringwise
  comparison, and does not distinguish defined versus undefined empty strings.
  Modify if you have other needs.
<div class="Pp"></div>
<pre>
    $are_equal = compare_arrays(\@frogs, \@toads);
    sub compare_arrays {
        my ($first, $second) = @_;
        no warnings;  # silence spurious -w undef complaints
        return 0 unless @$first == @$second;
        for (my $i = 0; $i &lt; @$first; $i++) {
            return 0 if $first-&gt;[$i] ne $second-&gt;[$i];
        }
        return 1;
    }
</pre>
<div class="Pp"></div>
For multilevel structures, you may wish to use an approach more like this one.
  It uses the CPAN module FreezeThaw:
<div class="Pp"></div>
<pre>
    use FreezeThaw qw(cmpStr);
    my @a = my @b = ( &quot;this&quot;, &quot;that&quot;, [ &quot;more&quot;, &quot;stuff&quot; ] );
    printf &quot;a and b contain %s arrays\n&quot;,
        cmpStr(\@a, \@b) == 0
        ? &quot;the same&quot;
        : &quot;different&quot;;
</pre>
<div class="Pp"></div>
This approach also works for comparing hashes. Here we'll demonstrate two
  different answers:
<div class="Pp"></div>
<pre>
    use FreezeThaw qw(cmpStr cmpStrHard);
    my %a = my %b = ( &quot;this&quot; =&gt; &quot;that&quot;, &quot;extra&quot; =&gt; [ &quot;more&quot;, &quot;stuff&quot; ] );
    $a{EXTRA} = \%b;
    $b{EXTRA} = \%a;
    printf &quot;a and b contain %s hashes\n&quot;,
    cmpStr(\%a, \%b) == 0 ? &quot;the same&quot; : &quot;different&quot;;
    printf &quot;a and b contain %s hashes\n&quot;,
    cmpStrHard(\%a, \%b) == 0 ? &quot;the same&quot; : &quot;different&quot;;
</pre>
<div class="Pp"></div>
The first reports that both those the hashes contain the same data, while the
  second reports that they do not. Which you prefer is left as an exercise to
  the reader.
<h2 class="Ss" title="Ss" id="How_do_I_find_the_first_array_element_for_which_a_condition_is_true?"><a class="selflink" href="#How_do_I_find_the_first_array_element_for_which_a_condition_is_true?">How
  do I find the first array element for which a condition is true?</a></h2>
To find the first array element which satisfies a condition, you can use the
  &quot;first()&quot; function in the List::Util module, which comes with Perl
  5.8. This example finds the first element that contains &quot;Perl&quot;.
<div class="Pp"></div>
<pre>
    use List::Util qw(first);
    my $element = first { /Perl/ } @array;
</pre>
<div class="Pp"></div>
If you cannot use List::Util, you can make your own loop to do the same thing.
  Once you find the element, you stop the loop with last.
<div class="Pp"></div>
<pre>
    my $found;
    foreach ( @array ) {
        if( /Perl/ ) { $found = $_; last }
    }
</pre>
<div class="Pp"></div>
If you want the array index, use the &quot;firstidx()&quot; function from
  &quot;List::MoreUtils&quot;:
<div class="Pp"></div>
<pre>
    use List::MoreUtils qw(firstidx);
    my $index = firstidx { /Perl/ } @array;
</pre>
<div class="Pp"></div>
Or write it yourself, iterating through the indices and checking the array
  element at each index until you find one that satisfies the condition:
<div class="Pp"></div>
<pre>
    my( $found, $index ) = ( undef, -1 );
    for( $i = 0; $i &lt; @array; $i++ ) {
        if( $array[$i] =~ /Perl/ ) {
            $found = $array[$i];
            $index = $i;
            last;
        }
    }
</pre>
<h2 class="Ss" title="Ss" id="How_do_I_handle_linked_lists?"><a class="selflink" href="#How_do_I_handle_linked_lists?">How
  do I handle linked lists?</a></h2>
(contributed by brian d foy)
<div class="Pp"></div>
Perl's arrays do not have a fixed size, so you don't need linked lists if you
  just want to add or remove items. You can use array operations such as
  &quot;push&quot;, &quot;pop&quot;, &quot;shift&quot;, &quot;unshift&quot;, or
  &quot;splice&quot; to do that.
<div class="Pp"></div>
Sometimes, however, linked lists can be useful in situations where you want to
  &quot;shard&quot; an array so you have have many small arrays instead of a
  single big array. You can keep arrays longer than Perl's largest array index,
  lock smaller arrays separately in threaded programs, reallocate less memory,
  or quickly insert elements in the middle of the chain.
<div class="Pp"></div>
Steve Lembark goes through the details in his YAPC::NA 2009 talk &quot;Perly
  Linked Lists&quot; ( http://www.slideshare.net/lembark/perly-linked-lists
  &lt;http://www.slideshare.net/lembark/perly-linked-lists&gt; ), although you
  can just use his LinkedList::Single module.
<h2 class="Ss" title="Ss" id="How_do_I_handle_circular_lists?"><a class="selflink" href="#How_do_I_handle_circular_lists?">How
  do I handle circular lists?</a></h2>
(contributed by brian d foy)
<div class="Pp"></div>
If you want to cycle through an array endlessly, you can increment the index
  modulo the number of elements in the array:
<div class="Pp"></div>
<pre>
    my @array = qw( a b c );
    my $i = 0;
    while( 1 ) {
        print $array[ $i++ % @array ], &quot;\n&quot;;
        last if $i &gt; 20;
    }
</pre>
<div class="Pp"></div>
You can also use Tie::Cycle to use a scalar that always has the next element of
  the circular array:
<div class="Pp"></div>
<pre>
    use Tie::Cycle;
    tie my $cycle, 'Tie::Cycle', [ qw( FFFFFF 000000 FFFF00 ) ];
    print $cycle; # FFFFFF
    print $cycle; # 000000
    print $cycle; # FFFF00
</pre>
<div class="Pp"></div>
The Array::Iterator::Circular creates an iterator object for circular arrays:
<div class="Pp"></div>
<pre>
    use Array::Iterator::Circular;
    my $color_iterator = Array::Iterator::Circular-&gt;new(
        qw(red green blue orange)
        );
    foreach ( 1 .. 20 ) {
        print $color_iterator-&gt;next, &quot;\n&quot;;
    }
</pre>
<h2 class="Ss" title="Ss" id="How_do_I_shuffle_an_array_randomly?"><a class="selflink" href="#How_do_I_shuffle_an_array_randomly?">How
  do I shuffle an array randomly?</a></h2>
If you either have Perl 5.8.0 or later installed, or if you have
  Scalar-List-Utils 1.03 or later installed, you can say:
<div class="Pp"></div>
<pre>
    use List::Util 'shuffle';
    @shuffled = shuffle(@list);
</pre>
<div class="Pp"></div>
If not, you can use a Fisher-Yates shuffle.
<div class="Pp"></div>
<pre>
    sub fisher_yates_shuffle {
        my $deck = shift;  # $deck is a reference to an array
        return unless @$deck; # must not be empty!
        my $i = @$deck;
        while (--$i) {
            my $j = int rand ($i+1);
            @$deck[$i,$j] = @$deck[$j,$i];
        }
    }
    # shuffle my mpeg collection
    #
    my @mpeg = &lt;audio/*/*.mp3&gt;;
    fisher_yates_shuffle( \@mpeg );    # randomize @mpeg in place
    print @mpeg;
</pre>
<div class="Pp"></div>
Note that the above implementation shuffles an array in place, unlike the
  &quot;List::Util::shuffle()&quot; which takes a list and returns a new
  shuffled list.
<div class="Pp"></div>
You've probably seen shuffling algorithms that work using splice, randomly
  picking another element to swap the current element with
<div class="Pp"></div>
<pre>
    srand;
    @new = ();
    @old = 1 .. 10;  # just a demo
    while (@old) {
        push(@new, splice(@old, rand @old, 1));
    }
</pre>
<div class="Pp"></div>
This is bad because splice is already O(N), and since you do it N times, you
  just invented a quadratic algorithm; that is, O(N**2). This does not scale,
  although Perl is so efficient that you probably won't notice this until you
  have rather largish arrays.
<h2 class="Ss" title="Ss" id="How_do_I_process/modify_each_element_of_an_array?"><a class="selflink" href="#How_do_I_process/modify_each_element_of_an_array?">How
  do I process/modify each element of an array?</a></h2>
Use &quot;for&quot;/&quot;foreach&quot;:
<div class="Pp"></div>
<pre>
    for (@lines) {
        s/foo/bar/;    # change that word
        tr/XZ/ZX/;    # swap those letters
    }
</pre>
<div class="Pp"></div>
Here's another; let's compute spherical volumes:
<div class="Pp"></div>
<pre>
    my @volumes = @radii;
    for (@volumes) {   # @volumes has changed parts
        $_ **= 3;
        $_ *= (4/3) * 3.14159;  # this will be constant folded
    }
</pre>
<div class="Pp"></div>
which can also be done with &quot;map()&quot; which is made to transform one
  list into another:
<div class="Pp"></div>
<pre>
    my @volumes = map {$_ ** 3 * (4/3) * 3.14159} @radii;
</pre>
<div class="Pp"></div>
If you want to do the same thing to modify the values of the hash, you can use
  the &quot;values&quot; function. As of Perl 5.6 the values are not copied, so
  if you modify $orbit (in this case), you modify the value.
<div class="Pp"></div>
<pre>
    for my $orbit ( values %orbits ) {
        ($orbit **= 3) *= (4/3) * 3.14159;
    }
</pre>
<div class="Pp"></div>
Prior to perl 5.6 &quot;values&quot; returned copies of the values, so older
  perl code often contains constructions such as @orbits{keys %orbits} instead
  of &quot;values %orbits&quot; where the hash is to be modified.
<h2 class="Ss" title="Ss" id="How_do_I_select_a_random_element_from_an_array?"><a class="selflink" href="#How_do_I_select_a_random_element_from_an_array?">How
  do I select a random element from an array?</a></h2>
Use the &quot;rand()&quot; function (see &quot;rand&quot; in perlfunc):
<div class="Pp"></div>
<pre>
    my $index   = rand @array;
    my $element = $array[$index];
</pre>
<div class="Pp"></div>
Or, simply:
<div class="Pp"></div>
<pre>
    my $element = $array[ rand @array ];
</pre>
<h2 class="Ss" title="Ss" id="How_do_I_permute_N_elements_of_a_list?"><a class="selflink" href="#How_do_I_permute_N_elements_of_a_list?">How
  do I permute N elements of a list?</a></h2>
Use the List::Permutor module on CPAN. If the list is actually an array, try the
  Algorithm::Permute module (also on CPAN). It's written in XS code and is very
  efficient:
<div class="Pp"></div>
<pre>
    use Algorithm::Permute;
    my @array = 'a'..'d';
    my $p_iterator = Algorithm::Permute-&gt;new ( \@array );
    while (my @perm = $p_iterator-&gt;next) {
       print &quot;next permutation: (@perm)\n&quot;;
    }
</pre>
<div class="Pp"></div>
For even faster execution, you could do:
<div class="Pp"></div>
<pre>
    use Algorithm::Permute;
    my @array = 'a'..'d';
    Algorithm::Permute::permute {
        print &quot;next permutation: (@array)\n&quot;;
    } @array;
</pre>
<div class="Pp"></div>
Here's a little program that generates all permutations of all the words on each
  line of input. The algorithm embodied in the &quot;permute()&quot; function is
  discussed in Volume 4 (still unpublished) of Knuth's <i>The Art of Computer
  Programming</i> and will work on any list:
<div class="Pp"></div>
<pre>
    #!/usr/bin/perl -n
    # Fischer-Krause ordered permutation generator
    sub permute (&amp;@) {
        my $code = shift;
        my @idx = 0..$#_;
        while ( $code-&gt;(@_[@idx]) ) {
            my $p = $#idx;
            --$p while $idx[$p-1] &gt; $idx[$p];
            my $q = $p or return;
            push @idx, reverse splice @idx, $p;
            ++$q while $idx[$p-1] &gt; $idx[$q];
            @idx[$p-1,$q]=@idx[$q,$p-1];
        }
    }
    permute { print &quot;@_\n&quot; } split;
</pre>
<div class="Pp"></div>
The Algorithm::Loops module also provides the &quot;NextPermute&quot; and
  &quot;NextPermuteNum&quot; functions which efficiently find all unique
  permutations of an array, even if it contains duplicate values, modifying it
  in-place: if its elements are in reverse-sorted order then the array is
  reversed, making it sorted, and it returns false; otherwise the next
  permutation is returned.
<div class="Pp"></div>
&quot;NextPermute&quot; uses string order and &quot;NextPermuteNum&quot; numeric
  order, so you can enumerate all the permutations of 0..9 like this:
<div class="Pp"></div>
<pre>
    use Algorithm::Loops qw(NextPermuteNum);
    my @list= 0..9;
    do { print &quot;@list\n&quot; } while NextPermuteNum @list;
</pre>
<h2 class="Ss" title="Ss" id="How_do_I_sort_an_array_by_(anything)?"><a class="selflink" href="#How_do_I_sort_an_array_by_(anything)?">How
  do I sort an array by (anything)?</a></h2>
Supply a comparison function to <i>sort()</i> (described in &quot;sort&quot; in
  perlfunc):
<div class="Pp"></div>
<pre>
    @list = sort { $a &lt;=&gt; $b } @list;
</pre>
<div class="Pp"></div>
The default sort function is cmp, string comparison, which would sort &quot;(1,
  2, 10)&quot; into &quot;(1, 10, 2)&quot;. &quot;&lt;=&gt;&quot;, used above,
  is the numerical comparison operator.
<div class="Pp"></div>
If you have a complicated function needed to pull out the part you want to sort
  on, then don't do it inside the sort function. Pull it out first, because the
  sort BLOCK can be called many times for the same element. Here's an example of
  how to pull out the first word after the first number on each item, and then
  sort those words case-insensitively.
<div class="Pp"></div>
<pre>
    my @idx;
    for (@data) {
        my $item;
        ($item) = /\d+\s*(\S+)/;
        push @idx, uc($item);
    }
    my @sorted = @data[ sort { $idx[$a] cmp $idx[$b] } 0 .. $#idx ];
</pre>
<div class="Pp"></div>
which could also be written this way, using a trick that's come to be known as
  the Schwartzian Transform:
<div class="Pp"></div>
<pre>
    my @sorted = map  { $_-&gt;[0] }
        sort { $a-&gt;[1] cmp $b-&gt;[1] }
        map  { [ $_, uc( (/\d+\s*(\S+)/)[0]) ] } @data;
</pre>
<div class="Pp"></div>
If you need to sort on several fields, the following paradigm is useful.
<div class="Pp"></div>
<pre>
    my @sorted = sort {
        field1($a) &lt;=&gt; field1($b) ||
        field2($a) cmp field2($b) ||
        field3($a) cmp field3($b)
    } @data;
</pre>
<div class="Pp"></div>
This can be conveniently combined with precalculation of keys as given above.
<div class="Pp"></div>
See the <i>sort</i> article in the &quot;Far More Than You Ever Wanted To
  Know&quot; collection in &lt;http://www.cpan.org/misc/olddoc/FMTEYEWTK.tgz&gt;
  for more about this approach.
<div class="Pp"></div>
See also the question later in perlfaq4 on sorting hashes.
<h2 class="Ss" title="Ss" id="How_do_I_manipulate_arrays_of_bits?"><a class="selflink" href="#How_do_I_manipulate_arrays_of_bits?">How
  do I manipulate arrays of bits?</a></h2>
Use &quot;pack()&quot; and &quot;unpack()&quot;, or else &quot;vec()&quot; and
  the bitwise operations.
<div class="Pp"></div>
For example, you don't have to store individual bits in an array (which would
  mean that you're wasting a lot of space). To convert an array of bits to a
  string, use &quot;vec()&quot; to set the right bits. This sets $vec to have
  bit N set only if $ints[N] was set:
<div class="Pp"></div>
<pre>
    my @ints = (...); # array of bits, e.g. ( 1, 0, 0, 1, 1, 0 ... )
    my $vec = '';
    foreach( 0 .. $#ints ) {
        vec($vec,$_,1) = 1 if $ints[$_];
    }
</pre>
<div class="Pp"></div>
The string $vec only takes up as many bits as it needs. For instance, if you had
  16 entries in @ints, $vec only needs two bytes to store them (not counting the
  scalar variable overhead).
<div class="Pp"></div>
Here's how, given a vector in $vec, you can get those bits into your @ints
  array:
<div class="Pp"></div>
<pre>
    sub bitvec_to_list {
        my $vec = shift;
        my @ints;
        # Find null-byte density then select best algorithm
        if ($vec =~ tr/\0// / length $vec &gt; 0.95) {
            use integer;
            my $i;
            # This method is faster with mostly null-bytes
            while($vec =~ /[^\0]/g ) {
                $i = -9 + 8 * pos $vec;
                push @ints, $i if vec($vec, ++$i, 1);
                push @ints, $i if vec($vec, ++$i, 1);
                push @ints, $i if vec($vec, ++$i, 1);
                push @ints, $i if vec($vec, ++$i, 1);
                push @ints, $i if vec($vec, ++$i, 1);
                push @ints, $i if vec($vec, ++$i, 1);
                push @ints, $i if vec($vec, ++$i, 1);
                push @ints, $i if vec($vec, ++$i, 1);
            }
        }
        else {
            # This method is a fast general algorithm
            use integer;
            my $bits = unpack &quot;b*&quot;, $vec;
            push @ints, 0 if $bits =~ s/^(\d)// &amp;&amp; $1;
            push @ints, pos $bits while($bits =~ /1/g);
        }
        return \@ints;
    }
</pre>
<div class="Pp"></div>
This method gets faster the more sparse the bit vector is. (Courtesy of Tim
  Bunce and Winfried Koenig.)
<div class="Pp"></div>
You can make the while loop a lot shorter with this suggestion from Benjamin
  Goldberg:
<div class="Pp"></div>
<pre>
    while($vec =~ /[^\0]+/g ) {
        push @ints, grep vec($vec, $_, 1), $-[0] * 8 .. $+[0] * 8;
    }
</pre>
<div class="Pp"></div>
Or use the CPAN module Bit::Vector:
<div class="Pp"></div>
<pre>
    my $vector = Bit::Vector-&gt;new($num_of_bits);
    $vector-&gt;Index_List_Store(@ints);
    my @ints = $vector-&gt;Index_List_Read();
</pre>
<div class="Pp"></div>
Bit::Vector provides efficient methods for bit vector, sets of small integers
  and &quot;big int&quot; math.
<div class="Pp"></div>
Here's a more extensive illustration using <i>vec()</i>:
<div class="Pp"></div>
<pre>
    # vec demo
    my $vector = &quot;\xff\x0f\xef\xfe&quot;;
    print &quot;Ilya's string \\xff\\x0f\\xef\\xfe represents the number &quot;,
    unpack(&quot;N&quot;, $vector), &quot;\n&quot;;
    my $is_set = vec($vector, 23, 1);
    print &quot;Its 23rd bit is &quot;, $is_set ? &quot;set&quot; : &quot;clear&quot;, &quot;.\n&quot;;
    pvec($vector);
    set_vec(1,1,1);
    set_vec(3,1,1);
    set_vec(23,1,1);
    set_vec(3,1,3);
    set_vec(3,2,3);
    set_vec(3,4,3);
    set_vec(3,4,7);
    set_vec(3,8,3);
    set_vec(3,8,7);
    set_vec(0,32,17);
    set_vec(1,32,17);
    sub set_vec {
        my ($offset, $width, $value) = @_;
        my $vector = '';
        vec($vector, $offset, $width) = $value;
        print &quot;offset=$offset width=$width value=$value\n&quot;;
        pvec($vector);
    }
    sub pvec {
        my $vector = shift;
        my $bits = unpack(&quot;b*&quot;, $vector);
        my $i = 0;
        my $BASE = 8;
        print &quot;vector length in bytes: &quot;, length($vector), &quot;\n&quot;;
        @bytes = unpack(&quot;A8&quot; x length($vector), $bits);
        print &quot;bits are: @bytes\n\n&quot;;
    }
</pre>
<h2 class="Ss" title="Ss" id="Why_does_defined()_return_true_on_empty_arrays_and_hashes?"><a class="selflink" href="#Why_does_defined()_return_true_on_empty_arrays_and_hashes?">Why
  does <i>defined()</i> return true on empty arrays and hashes?</a></h2>
The short story is that you should probably only use defined on scalars or
  functions, not on aggregates (arrays and hashes). See &quot;defined&quot; in
  perlfunc in the 5.004 release or later of Perl for more detail.
<h1 class="Sh" title="Sh" id="Data:_Hashes_(Associative_Arrays)"><a class="selflink" href="#Data:_Hashes_(Associative_Arrays)">Data:
  Hashes (Associative Arrays)</a></h1>
<h2 class="Ss" title="Ss" id="How_do_I_process_an_entire_hash?"><a class="selflink" href="#How_do_I_process_an_entire_hash?">How
  do I process an entire hash?</a></h2>
(contributed by brian d foy)
<div class="Pp"></div>
There are a couple of ways that you can process an entire hash. You can get a
  list of keys, then go through each key, or grab a one key-value pair at a
  time.
<div class="Pp"></div>
To go through all of the keys, use the &quot;keys&quot; function. This extracts
  all of the keys of the hash and gives them back to you as a list. You can then
  get the value through the particular key you're processing:
<div class="Pp"></div>
<pre>
    foreach my $key ( keys %hash ) {
        my $value = $hash{$key}
        ...
    }
</pre>
<div class="Pp"></div>
Once you have the list of keys, you can process that list before you process the
  hash elements. For instance, you can sort the keys so you can process them in
  lexical order:
<div class="Pp"></div>
<pre>
    foreach my $key ( sort keys %hash ) {
        my $value = $hash{$key}
        ...
    }
</pre>
<div class="Pp"></div>
Or, you might want to only process some of the items. If you only want to deal
  with the keys that start with &quot;text:&quot;, you can select just those
  using &quot;grep&quot;:
<div class="Pp"></div>
<pre>
    foreach my $key ( grep /^text:/, keys %hash ) {
        my $value = $hash{$key}
        ...
    }
</pre>
<div class="Pp"></div>
If the hash is very large, you might not want to create a long list of keys. To
  save some memory, you can grab one key-value pair at a time using
  &quot;each()&quot;, which returns a pair you haven't seen yet:
<div class="Pp"></div>
<pre>
    while( my( $key, $value ) = each( %hash ) ) {
        ...
    }
</pre>
<div class="Pp"></div>
The &quot;each&quot; operator returns the pairs in apparently random order, so
  if ordering matters to you, you'll have to stick with the &quot;keys&quot;
  method.
<div class="Pp"></div>
The &quot;each()&quot; operator can be a bit tricky though. You can't add or
  delete keys of the hash while you're using it without possibly skipping or
  re-processing some pairs after Perl internally rehashes all of the elements.
  Additionally, a hash has only one iterator, so if you mix &quot;keys&quot;,
  &quot;values&quot;, or &quot;each&quot; on the same hash, you risk resetting
  the iterator and messing up your processing. See the &quot;each&quot; entry in
  perlfunc for more details.
<h2 class="Ss" title="Ss" id="How_do_I_merge_two_hashes?"><a class="selflink" href="#How_do_I_merge_two_hashes?">How
  do I merge two hashes?</a></h2>
(contributed by brian d foy)
<div class="Pp"></div>
Before you decide to merge two hashes, you have to decide what to do if both
  hashes contain keys that are the same and if you want to leave the original
  hashes as they were.
<div class="Pp"></div>
If you want to preserve the original hashes, copy one hash (%hash1) to a new
  hash (%new_hash), then add the keys from the other hash (%hash2 to the new
  hash. Checking that the key already exists in %new_hash gives you a chance to
  decide what to do with the duplicates:
<div class="Pp"></div>
<pre>
    my %new_hash = %hash1; # make a copy; leave %hash1 alone
    foreach my $key2 ( keys %hash2 ) {
        if( exists $new_hash{$key2} ) {
            warn &quot;Key [$key2] is in both hashes!&quot;;
            # handle the duplicate (perhaps only warning)
            ...
            next;
        }
        else {
            $new_hash{$key2} = $hash2{$key2};
        }
    }
</pre>
<div class="Pp"></div>
If you don't want to create a new hash, you can still use this looping
  technique; just change the %new_hash to %hash1.
<div class="Pp"></div>
<pre>
    foreach my $key2 ( keys %hash2 ) {
        if( exists $hash1{$key2} ) {
            warn &quot;Key [$key2] is in both hashes!&quot;;
            # handle the duplicate (perhaps only warning)
            ...
            next;
        }
        else {
            $hash1{$key2} = $hash2{$key2};
        }
      }
</pre>
<div class="Pp"></div>
If you don't care that one hash overwrites keys and values from the other, you
  could just use a hash slice to add one hash to another. In this case, values
  from %hash2 replace values from %hash1 when they have keys in common:
<div class="Pp"></div>
<pre>
    @hash1{ keys %hash2 } = values %hash2;
</pre>
<h2 class="Ss" title="Ss" id="What_happens_if_I_add_or_remove_keys_from_a_hash_while_iterating_over_it?"><a class="selflink" href="#What_happens_if_I_add_or_remove_keys_from_a_hash_while_iterating_over_it?">What
  happens if I add or remove keys from a hash while iterating over it?</a></h2>
(contributed by brian d foy)
<div class="Pp"></div>
The easy answer is &quot;Don't do that!&quot;
<div class="Pp"></div>
If you iterate through the hash with <i>each()</i>, you can delete the key most
  recently returned without worrying about it. If you delete or add other keys,
  the iterator may skip or double up on them since perl may rearrange the hash
  table. See the entry for &quot;each()&quot; in perlfunc.
<h2 class="Ss" title="Ss" id="How_do_I_look_up_a_hash_element_by_value?"><a class="selflink" href="#How_do_I_look_up_a_hash_element_by_value?">How
  do I look up a hash element by value?</a></h2>
Create a reverse hash:
<div class="Pp"></div>
<pre>
    my %by_value = reverse %by_key;
    my $key = $by_value{$value};
</pre>
<div class="Pp"></div>
That's not particularly efficient. It would be more space-efficient to use:
<div class="Pp"></div>
<pre>
    while (my ($key, $value) = each %by_key) {
        $by_value{$value} = $key;
    }
</pre>
<div class="Pp"></div>
If your hash could have repeated values, the methods above will only find one of
  the associated keys. This may or may not worry you. If it does worry you, you
  can always reverse the hash into a hash of arrays instead:
<div class="Pp"></div>
<pre>
    while (my ($key, $value) = each %by_key) {
         push @{$key_list_by_value{$value}}, $key;
    }
</pre>
<h2 class="Ss" title="Ss" id="How_can_I_know_how_many_entries_are_in_a_hash?"><a class="selflink" href="#How_can_I_know_how_many_entries_are_in_a_hash?">How
  can I know how many entries are in a hash?</a></h2>
(contributed by brian d foy)
<div class="Pp"></div>
This is very similar to &quot;How do I process an entire hash?&quot;, also in
  perlfaq4, but a bit simpler in the common cases.
<div class="Pp"></div>
You can use the &quot;keys()&quot; built-in function in scalar context to find
  out have many entries you have in a hash:
<div class="Pp"></div>
<pre>
    my $key_count = keys %hash; # must be scalar context!
</pre>
<div class="Pp"></div>
If you want to find out how many entries have a defined value, that's a bit
  different. You have to check each value. A &quot;grep&quot; is handy:
<div class="Pp"></div>
<pre>
    my $defined_value_count = grep { defined } values %hash;
</pre>
<div class="Pp"></div>
You can use that same structure to count the entries any way that you like. If
  you want the count of the keys with vowels in them, you just test for that
  instead:
<div class="Pp"></div>
<pre>
    my $vowel_count = grep { /[aeiou]/ } keys %hash;
</pre>
<div class="Pp"></div>
The &quot;grep&quot; in scalar context returns the count. If you want the list
  of matching items, just use it in list context instead:
<div class="Pp"></div>
<pre>
    my @defined_values = grep { defined } values %hash;
</pre>
<div class="Pp"></div>
The &quot;keys()&quot; function also resets the iterator, which means that you
  may see strange results if you use this between uses of other hash operators
  such as &quot;each()&quot;.
<h2 class="Ss" title="Ss" id="How_do_I_sort_a_hash_(optionally_by_value_instead_of_key)?"><a class="selflink" href="#How_do_I_sort_a_hash_(optionally_by_value_instead_of_key)?">How
  do I sort a hash (optionally by value instead of key)?</a></h2>
(contributed by brian d foy)
<div class="Pp"></div>
To sort a hash, start with the keys. In this example, we give the list of keys
  to the sort function which then compares them ASCIIbetically (which might be
  affected by your locale settings). The output list has the keys in
  ASCIIbetical order. Once we have the keys, we can go through them to create a
  report which lists the keys in ASCIIbetical order.
<div class="Pp"></div>
<pre>
    my @keys = sort { $a cmp $b } keys %hash;
    foreach my $key ( @keys ) {
        printf &quot;%-20s %6d\n&quot;, $key, $hash{$key};
    }
</pre>
<div class="Pp"></div>
We could get more fancy in the &quot;sort()&quot; block though. Instead of
  comparing the keys, we can compute a value with them and use that value as the
  comparison.
<div class="Pp"></div>
For instance, to make our report order case-insensitive, we use &quot;lc&quot;
  to lowercase the keys before comparing them:
<div class="Pp"></div>
<pre>
    my @keys = sort { lc $a cmp lc $b } keys %hash;
</pre>
<div class="Pp"></div>
Note: if the computation is expensive or the hash has many elements, you may
  want to look at the Schwartzian Transform to cache the computation results.
<div class="Pp"></div>
If we want to sort by the hash value instead, we use the hash key to look it up.
  We still get out a list of keys, but this time they are ordered by their
  value.
<div class="Pp"></div>
<pre>
    my @keys = sort { $hash{$a} &lt;=&gt; $hash{$b} } keys %hash;
</pre>
<div class="Pp"></div>
From there we can get more complex. If the hash values are the same, we can
  provide a secondary sort on the hash key.
<div class="Pp"></div>
<pre>
    my @keys = sort {
        $hash{$a} &lt;=&gt; $hash{$b}
            or
        &quot;\L$a&quot; cmp &quot;\L$b&quot;
    } keys %hash;
</pre>
<h2 class="Ss" title="Ss" id="How_can_I_always_keep_my_hash_sorted?"><a class="selflink" href="#How_can_I_always_keep_my_hash_sorted?">How
  can I always keep my hash sorted?</a></h2>
You can look into using the &quot;DB_File&quot; module and &quot;tie()&quot;
  using the $DB_BTREE hash bindings as documented in &quot;In Memory
  Databases&quot; in DB_File. The Tie::IxHash module from CPAN might also be
  instructive. Although this does keep your hash sorted, you might not like the
  slowdown you suffer from the tie interface. Are you sure you need to do this?
  :)
<h2 class="Ss" title="Ss" id="What's_the_difference_between_&quot;delete&quot;_and_&quot;undef&quot;_with_hashes?"><a class="selflink" href="#What's_the_difference_between_&quot;delete&quot;_and_&quot;undef&quot;_with_hashes?">What's
  the difference between &quot;delete&quot; and &quot;undef&quot; with
  hashes?</a></h2>
Hashes contain pairs of scalars: the first is the key, the second is the value.
  The key will be coerced to a string, although the value can be any kind of
  scalar: string, number, or reference. If a key $key is present in %hash,
  &quot;exists($hash{$key})&quot; will return true. The value for a given key
  can be &quot;undef&quot;, in which case $hash{$key} will be &quot;undef&quot;
  while &quot;exists $hash{$key}&quot; will return true. This corresponds to
  ($key, &quot;undef&quot;) being in the hash.
<div class="Pp"></div>
Pictures help... Here's the %hash table:
<div class="Pp"></div>
<pre>
      keys  values
    +------+------+
    |  a   |  3   |
    |  x   |  7   |
    |  d   |  0   |
    |  e   |  2   |
    +------+------+
</pre>
<div class="Pp"></div>
And these conditions hold
<div class="Pp"></div>
<pre>
    $hash{'a'}                       is true
    $hash{'d'}                       is false
    defined $hash{'d'}               is true
    defined $hash{'a'}               is true
    exists $hash{'a'}                is true (Perl 5 only)
    grep ($_ eq 'a', keys %hash)     is true
</pre>
<div class="Pp"></div>
If you now say
<div class="Pp"></div>
<pre>
    undef $hash{'a'}
</pre>
<div class="Pp"></div>
your table now reads:
<div class="Pp"></div>
<pre>
      keys  values
    +------+------+
    |  a   | undef|
    |  x   |  7   |
    |  d   |  0   |
    |  e   |  2   |
    +------+------+
</pre>
<div class="Pp"></div>
and these conditions now hold; changes in caps:
<div class="Pp"></div>
<pre>
    $hash{'a'}                       is FALSE
    $hash{'d'}                       is false
    defined $hash{'d'}               is true
    defined $hash{'a'}               is FALSE
    exists $hash{'a'}                is true (Perl 5 only)
    grep ($_ eq 'a', keys %hash)     is true
</pre>
<div class="Pp"></div>
Notice the last two: you have an undef value, but a defined key!
<div class="Pp"></div>
Now, consider this:
<div class="Pp"></div>
<pre>
    delete $hash{'a'}
</pre>
<div class="Pp"></div>
your table now reads:
<div class="Pp"></div>
<pre>
      keys  values
    +------+------+
    |  x   |  7   |
    |  d   |  0   |
    |  e   |  2   |
    +------+------+
</pre>
<div class="Pp"></div>
and these conditions now hold; changes in caps:
<div class="Pp"></div>
<pre>
    $hash{'a'}                       is false
    $hash{'d'}                       is false
    defined $hash{'d'}               is true
    defined $hash{'a'}               is false
    exists $hash{'a'}                is FALSE (Perl 5 only)
    grep ($_ eq 'a', keys %hash)     is FALSE
</pre>
<div class="Pp"></div>
See, the whole entry is gone!
<h2 class="Ss" title="Ss" id="Why_don't_my_tied_hashes_make_the_defined/exists_distinction?"><a class="selflink" href="#Why_don't_my_tied_hashes_make_the_defined/exists_distinction?">Why
  don't my tied hashes make the defined/exists distinction?</a></h2>
This depends on the tied hash's implementation of <i>EXISTS()</i>. For example,
  there isn't the concept of undef with hashes that are tied to DBM* files. It
  also means that <i>exists()</i> and <i>defined()</i> do the same thing with a
  DBM* file, and what they end up doing is not what they do with ordinary
  hashes.
<h2 class="Ss" title="Ss" id="How_do_I_reset_an_each()_operation_part-way_through?"><a class="selflink" href="#How_do_I_reset_an_each()_operation_part-way_through?">How
  do I reset an <i>each()</i> operation part-way through?</a></h2>
(contributed by brian d foy)
<div class="Pp"></div>
You can use the &quot;keys&quot; or &quot;values&quot; functions to reset
  &quot;each&quot;. To simply reset the iterator used by &quot;each&quot;
  without doing anything else, use one of them in void context:
<div class="Pp"></div>
<pre>
    keys %hash; # resets iterator, nothing else.
    values %hash; # resets iterator, nothing else.
</pre>
<div class="Pp"></div>
See the documentation for &quot;each&quot; in perlfunc.
<h2 class="Ss" title="Ss" id="How_can_I_get_the_unique_keys_from_two_hashes?"><a class="selflink" href="#How_can_I_get_the_unique_keys_from_two_hashes?">How
  can I get the unique keys from two hashes?</a></h2>
First you extract the keys from the hashes into lists, then solve the
  &quot;removing duplicates&quot; problem described above. For example:
<div class="Pp"></div>
<pre>
    my %seen = ();
    for my $element (keys(%foo), keys(%bar)) {
        $seen{$element}++;
    }
    my @uniq = keys %seen;
</pre>
<div class="Pp"></div>
Or more succinctly:
<div class="Pp"></div>
<pre>
    my @uniq = keys %{{%foo,%bar}};
</pre>
<div class="Pp"></div>
Or if you really want to save space:
<div class="Pp"></div>
<pre>
    my %seen = ();
    while (defined ($key = each %foo)) {
        $seen{$key}++;
    }
    while (defined ($key = each %bar)) {
        $seen{$key}++;
    }
    my @uniq = keys %seen;
</pre>
<h2 class="Ss" title="Ss" id="How_can_I_store_a_multidimensional_array_in_a_DBM_file?"><a class="selflink" href="#How_can_I_store_a_multidimensional_array_in_a_DBM_file?">How
  can I store a multidimensional array in a DBM file?</a></h2>
Either stringify the structure yourself (no fun), or else get the MLDBM (which
  uses Data::Dumper) module from CPAN and layer it on top of either DB_File or
  GDBM_File. You might also try DBM::Deep, but it can be a bit slow.
<h2 class="Ss" title="Ss" id="How_can_I_make_my_hash_remember_the_order_I_put_elements_into_it?"><a class="selflink" href="#How_can_I_make_my_hash_remember_the_order_I_put_elements_into_it?">How
  can I make my hash remember the order I put elements into it?</a></h2>
Use the Tie::IxHash from CPAN.
<div class="Pp"></div>
<pre>
    use Tie::IxHash;
    tie my %myhash, 'Tie::IxHash';
    for (my $i=0; $i&lt;20; $i++) {
        $myhash{$i} = 2*$i;
    }
    my @keys = keys %myhash;
    # @keys = (0,1,2,3,...)
</pre>
<h2 class="Ss" title="Ss" id="Why_does_passing_a_subroutine_an_undefined_element_in_a_hash_create_it?"><a class="selflink" href="#Why_does_passing_a_subroutine_an_undefined_element_in_a_hash_create_it?">Why
  does passing a subroutine an undefined element in a hash create it?</a></h2>
(contributed by brian d foy)
<div class="Pp"></div>
Are you using a really old version of Perl?
<div class="Pp"></div>
Normally, accessing a hash key's value for a nonexistent key will <i>not</i>
  create the key.
<div class="Pp"></div>
<pre>
    my %hash  = ();
    my $value = $hash{ 'foo' };
    print &quot;This won't print\n&quot; if exists $hash{ 'foo' };
</pre>
<div class="Pp"></div>
Passing $hash{ 'foo' } to a subroutine used to be a special case, though. Since
  you could assign directly to $_[0], Perl had to be ready to make that
  assignment so it created the hash key ahead of time:
<div class="Pp"></div>
<pre>
    my_sub( $hash{ 'foo' } );
    print &quot;This will print before 5.004\n&quot; if exists $hash{ 'foo' };
    sub my_sub {
        # $_[0] = 'bar'; # create hash key in case you do this
        1;
    }
</pre>
<div class="Pp"></div>
Since Perl 5.004, however, this situation is a special case and Perl creates the
  hash key only when you make the assignment:
<div class="Pp"></div>
<pre>
    my_sub( $hash{ 'foo' } );
    print &quot;This will print, even after 5.004\n&quot; if exists $hash{ 'foo' };
    sub my_sub {
        $_[0] = 'bar';
    }
</pre>
<div class="Pp"></div>
However, if you want the old behavior (and think carefully about that because
  it's a weird side effect), you can pass a hash slice instead. Perl 5.004
  didn't make this a special case:
<div class="Pp"></div>
<pre>
    my_sub( @hash{ qw/foo/ } );
</pre>
<h2 class="Ss" title="Ss" id="How_can_I_make_the_Perl_equivalent_of_a_C_structure/C++_class/hash_or_array_of_hashes_or_arrays?"><a class="selflink" href="#How_can_I_make_the_Perl_equivalent_of_a_C_structure/C++_class/hash_or_array_of_hashes_or_arrays?">How
  can I make the Perl equivalent of a C structure/C++ class/hash or array of
  hashes or arrays?</a></h2>
Usually a hash ref, perhaps like this:
<div class="Pp"></div>
<pre>
    $record = {
        NAME   =&gt; &quot;Jason&quot;,
        EMPNO  =&gt; 132,
        TITLE  =&gt; &quot;deputy peon&quot;,
        AGE    =&gt; 23,
        SALARY =&gt; 37_000,
        PALS   =&gt; [ &quot;Norbert&quot;, &quot;Rhys&quot;, &quot;Phineas&quot;],
    };
</pre>
<div class="Pp"></div>
References are documented in perlref and perlreftut. Examples of complex data
  structures are given in perldsc and perllol. Examples of structures and
  object-oriented classes are in perltoot.
<h2 class="Ss" title="Ss" id="How_can_I_use_a_reference_as_a_hash_key?"><a class="selflink" href="#How_can_I_use_a_reference_as_a_hash_key?">How
  can I use a reference as a hash key?</a></h2>
(contributed by brian d foy and Ben Morrow)
<div class="Pp"></div>
Hash keys are strings, so you can't really use a reference as the key. When you
  try to do that, perl turns the reference into its stringified form (for
  instance, &quot;HASH(0xDEADBEEF)&quot;). From there you can't get back the
  reference from the stringified form, at least without doing some extra work on
  your own.
<div class="Pp"></div>
Remember that the entry in the hash will still be there even if the referenced
  variable goes out of scope, and that it is entirely possible for Perl to
  subsequently allocate a different variable at the same address. This will mean
  a new variable might accidentally be associated with the value for an old.
<div class="Pp"></div>
If you have Perl 5.10 or later, and you just want to store a value against the
  reference for lookup later, you can use the core Hash::Util::Fieldhash module.
  This will also handle renaming the keys if you use multiple threads (which
  causes all variables to be reallocated at new addresses, changing their
  stringification), and garbage-collecting the entries when the referenced
  variable goes out of scope.
<div class="Pp"></div>
If you actually need to be able to get a real reference back from each hash
  entry, you can use the Tie::RefHash module, which does the required work for
  you.
<h2 class="Ss" title="Ss" id="How_can_I_check_if_a_key_exists_in_a_multilevel_hash?"><a class="selflink" href="#How_can_I_check_if_a_key_exists_in_a_multilevel_hash?">How
  can I check if a key exists in a multilevel hash?</a></h2>
(contributed by brian d foy)
<div class="Pp"></div>
The trick to this problem is avoiding accidental autovivification. If you want
  to check three keys deep, you might naievely try this:
<div class="Pp"></div>
<pre>
    my %hash;
    if( exists $hash{key1}{key2}{key3} ) {
        ...;
    }
</pre>
<div class="Pp"></div>
Even though you started with a completely empty hash, after that call to
  &quot;exists&quot; you've created the structure you needed to check for
  &quot;key3&quot;:
<div class="Pp"></div>
<pre>
    %hash = (
              'key1' =&gt; {
                          'key2' =&gt; {}
                        }
            );
</pre>
<div class="Pp"></div>
That's autovivification. You can get around this in a few ways. The easiest way
  is to just turn it off. The lexical &quot;autovivification&quot; pragma is
  available on CPAN. Now you don't add to the hash:
<div class="Pp"></div>
<pre>
    {
        no autovivification;
        my %hash;
        if( exists $hash{key1}{key2}{key3} ) {
            ...;
        }
    }
</pre>
<div class="Pp"></div>
The Data::Diver module on CPAN can do it for you too. Its &quot;Dive&quot;
  subroutine can tell you not only if the keys exist but also get the value:
<div class="Pp"></div>
<pre>
    use Data::Diver qw(Dive);
    my @exists = Dive( \%hash, qw(key1 key2 key3) );
    if(  ! @exists  ) {
        ...; # keys do not exist
    }
    elsif(  ! defined $exists[0]  ) {
        ...; # keys exist but value is undef
    }
</pre>
<div class="Pp"></div>
You can easily do this yourself too by checking each level of the hash before
  you move onto the next level. This is essentially what Data::Diver does for
  you:
<div class="Pp"></div>
<pre>
    if( check_hash( \%hash, qw(key1 key2 key3) ) ) {
        ...;
    }
    sub check_hash {
       my( $hash, @keys ) = @_;
       return unless @keys;
       foreach my $key ( @keys ) {
           return unless eval { exists $hash-&gt;{$key} };
           $hash = $hash-&gt;{$key};
        }
       return 1;
    }
</pre>
<h2 class="Ss" title="Ss" id="How_can_I_prevent_addition_of_unwanted_keys_into_a_hash?"><a class="selflink" href="#How_can_I_prevent_addition_of_unwanted_keys_into_a_hash?">How
  can I prevent addition of unwanted keys into a hash?</a></h2>
Since version 5.8.0, hashes can be <i>restricted</i> to a fixed number of given
  keys. Methods for creating and dealing with restricted hashes are exported by
  the Hash::Util module.
<h1 class="Sh" title="Sh" id="Data:_Misc"><a class="selflink" href="#Data:_Misc">Data:
  Misc</a></h1>
<h2 class="Ss" title="Ss" id="How_do_I_handle_binary_data_correctly?"><a class="selflink" href="#How_do_I_handle_binary_data_correctly?">How
  do I handle binary data correctly?</a></h2>
Perl is binary-clean, so it can handle binary data just fine. On Windows or DOS,
  however, you have to use &quot;binmode&quot; for binary files to avoid
  conversions for line endings. In general, you should use &quot;binmode&quot;
  any time you want to work with binary data.
<div class="Pp"></div>
Also see &quot;binmode&quot; in perlfunc or perlopentut.
<div class="Pp"></div>
If you're concerned about 8-bit textual data then see perllocale. If you want to
  deal with multibyte characters, however, there are some gotchas. See the
  section on Regular Expressions.
<h2 class="Ss" title="Ss" id="How_do_I_determine_whether_a_scalar_is_a_number/whole/integer/float?"><a class="selflink" href="#How_do_I_determine_whether_a_scalar_is_a_number/whole/integer/float?">How
  do I determine whether a scalar is a number/whole/integer/float?</a></h2>
Assuming that you don't care about IEEE notations like &quot;NaN&quot; or
  &quot;Infinity&quot;, you probably just want to use a regular expression:
<div class="Pp"></div>
<pre>
    use 5.010;
    given( $number ) {
        when( /\D/ )
            { say &quot;\thas nondigits&quot;; continue }
        when( /^\d+\z/ )
            { say &quot;\tis a whole number&quot;; continue }
        when( /^-?\d+\z/ )
            { say &quot;\tis an integer&quot;; continue }
        when( /^[+-]?\d+\z/ )
            { say &quot;\tis a +/- integer&quot;; continue }
        when( /^-?(?:\d+\.?|\.\d)\d*\z/ )
            { say &quot;\tis a real number&quot;; continue }
        when( /^[+-]?(?=\.?\d)\d*\.?\d*(?:e[+-]?\d+)?\z/i)
            { say &quot;\tis a C float&quot; }
    }
</pre>
<div class="Pp"></div>
There are also some commonly used modules for the task. Scalar::Util
  (distributed with 5.8) provides access to perl's internal function
  &quot;looks_like_number&quot; for determining whether a variable looks like a
  number. Data::Types exports functions that validate data types using both the
  above and other regular expressions. Thirdly, there is Regexp::Common which
  has regular expressions to match various types of numbers. Those three modules
  are available from the CPAN.
<div class="Pp"></div>
If you're on a POSIX system, Perl supports the &quot;POSIX::strtod&quot;
  function for converting strings to doubles (and also &quot;POSIX::strtol&quot;
  for longs). Its semantics are somewhat cumbersome, so here's a
  &quot;getnum&quot; wrapper function for more convenient access. This function
  takes a string and returns the number it found, or &quot;undef&quot; for input
  that isn't a C float. The &quot;is_numeric&quot; function is a front end to
  &quot;getnum&quot; if you just want to say, &quot;Is this a float?&quot;
<div class="Pp"></div>
<pre>
    sub getnum {
        use POSIX qw(strtod);
        my $str = shift;
        $str =~ s/^\s+//;
        $str =~ s/\s+$//;
        $! = 0;
        my($num, $unparsed) = strtod($str);
        if (($str eq '') || ($unparsed != 0) || $!) {
                return undef;
        }
        else {
            return $num;
        }
    }
    sub is_numeric { defined getnum($_[0]) }
</pre>
<div class="Pp"></div>
Or you could check out the String::Scanf module on the CPAN instead.
<h2 class="Ss" title="Ss" id="How_do_I_keep_persistent_data_across_program_calls?"><a class="selflink" href="#How_do_I_keep_persistent_data_across_program_calls?">How
  do I keep persistent data across program calls?</a></h2>
For some specific applications, you can use one of the DBM modules. See
  AnyDBM_File. More generically, you should consult the FreezeThaw or Storable
  modules from CPAN. Starting from Perl 5.8, Storable is part of the standard
  distribution. Here's one example using Storable's &quot;store&quot; and
  &quot;retrieve&quot; functions:
<div class="Pp"></div>
<pre>
    use Storable;
    store(\%hash, &quot;filename&quot;);
    # later on...
    $href = retrieve(&quot;filename&quot;);        # by ref
    %hash = %{ retrieve(&quot;filename&quot;) };   # direct to hash
</pre>
<h2 class="Ss" title="Ss" id="How_do_I_print_out_or_copy_a_recursive_data_structure?"><a class="selflink" href="#How_do_I_print_out_or_copy_a_recursive_data_structure?">How
  do I print out or copy a recursive data structure?</a></h2>
The Data::Dumper module on CPAN (or the 5.005 release of Perl) is great for
  printing out data structures. The Storable module on CPAN (or the 5.8 release
  of Perl), provides a function called &quot;dclone&quot; that recursively
  copies its argument.
<div class="Pp"></div>
<pre>
    use Storable qw(dclone);
    $r2 = dclone($r1);
</pre>
<div class="Pp"></div>
Where $r1 can be a reference to any kind of data structure you'd like. It will
  be deeply copied. Because &quot;dclone&quot; takes and returns references,
  you'd have to add extra punctuation if you had a hash of arrays that you
  wanted to copy.
<div class="Pp"></div>
<pre>
    %newhash = %{ dclone(\%oldhash) };
</pre>
<h2 class="Ss" title="Ss" id="How_do_I_define_methods_for_every_class/object?"><a class="selflink" href="#How_do_I_define_methods_for_every_class/object?">How
  do I define methods for every class/object?</a></h2>
(contributed by Ben Morrow)
<div class="Pp"></div>
You can use the &quot;UNIVERSAL&quot; class (see UNIVERSAL). However, please be
  very careful to consider the consequences of doing this: adding methods to
  every object is very likely to have unintended consequences. If possible, it
  would be better to have all your object inherit from some common base class,
  or to use an object system like Moose that supports roles.
<h2 class="Ss" title="Ss" id="How_do_I_verify_a_credit_card_checksum?"><a class="selflink" href="#How_do_I_verify_a_credit_card_checksum?">How
  do I verify a credit card checksum?</a></h2>
Get the Business::CreditCard module from CPAN.
<h2 class="Ss" title="Ss" id="How_do_I_pack_arrays_of_doubles_or_floats_for_XS_code?"><a class="selflink" href="#How_do_I_pack_arrays_of_doubles_or_floats_for_XS_code?">How
  do I pack arrays of doubles or floats for XS code?</a></h2>
The arrays.h/arrays.c code in the PGPLOT module on CPAN does just this. If
  you're doing a lot of float or double processing, consider using the PDL
  module from CPAN instead--it makes number-crunching easy.
<div class="Pp"></div>
See &lt;http://search.cpan.org/dist/PGPLOT&gt; for the code.
<h1 class="Sh" title="Sh" id="AUTHOR_AND_COPYRIGHT"><a class="selflink" href="#AUTHOR_AND_COPYRIGHT">AUTHOR
  AND COPYRIGHT</a></h1>
Copyright (c) 1997-2010 Tom Christiansen, Nathan Torkington, and other authors
  as noted. All rights reserved.
<div class="Pp"></div>
This documentation is free; you can redistribute it and/or modify it under the
  same terms as Perl itself.
<div class="Pp"></div>
Irrespective of its distribution, all code examples in this file are hereby
  placed into the public domain. You are permitted and encouraged to use this
  code in your own programs for fun or for profit as you see fit. A simple
  comment in the code giving credit would be courteous but is not
  required.</div>
<table class="foot">
  <tr>
    <td class="foot-date">2013-03-04</td>
    <td class="foot-os">perl v5.16.3</td>
  </tr>
</table>
</body>
</html>
